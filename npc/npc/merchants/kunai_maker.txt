//===== RoSF.us Script =======================================
//= Kunai Merchant Kashin
//===== By: ============Translation:==========================
//= rAthena Dev Team    安赫尔/Money[RoSF.us]20131006
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Trades a few shurikens + ninja stones for elemental kunai.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=     id of the 2 required items plus the amount. Can trade
//=     up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.3a Kagerou/Oboro support (BaseJob -> BaseClass) [Euphy]
//============================================================

que_ng,72,29,3	script	飞刀商人 卡新	83,{
	if(BaseClass == Job_Ninja) {
		mes "[卡新]";
		mes "我是卡新, 负责给";
		mes "忍者们分配短刀. ";
		mes "请随意查看并告诉我";
		mes "您是否对这些";
		mes "装备感兴趣. ";
		next;
		switch(select("- 10 猛毒短刀:- 10 寒气短刀:- 10 冷风短刀:- 10 黑土短刀:- 10 爆炎短刀:- 取消")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[卡新]";
			mes "很好, 谢谢您";
			mes "光临我的店. ";
			mes "如果需要买短刀";
			mes "随时可以来找我. ";
			mes "再会~";
			close;
		}
	}
	mes "[卡新]";
	mes "我是卡新, 负责给";
	mes "忍者们分配短刀. ";
	mes "如果您有忍者朋友, ";
	mes "那您可以告诉他们";
	mes "有关我的店的事情. ";
	close;
}

function	script	Kunai_Trade	{
	mes "[卡新]";
	mes "您可以用";
	mes ""+getarg(1)+" "+getitemname(getarg(0))+" 和";
	mes ""+getarg(3)+" "+getitemname(getarg(2))+" 来兑换";
	mes "每组 10 个 "+getitemname(getarg(4))+". ";
	next;
	mes "[卡新]";
	mes "我一次最多只能给您 500 组短刀. ";
	mes "如果您要取消交易, 只需要输入''0.''";
	mes "您想要买多少组短刀? ";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[卡新]";
		mes "呃? 对不起. ";
		mes "我不能给您那么多";
		mes "组短刀. 请重新输入";
		mes "小于 500 的数字. ";
		close;
	}
	if(countitem(getarg(0)) < .@amount*getarg(1) || countitem(getarg(2)) < .@amount*getarg(3)) {
		mes "[卡新]";
		mes "唔……您没有足够的";
		mes "材料来换取您需要的";
		mes "短刀. 请重新检查";
		mes "您所携带的物品. ";
		close;
 	}
	if(checkweight(getarg(4), .@amount*10) == 0) {
		mes "[卡新]";
		mes "唔……看来您的背包里";
		mes "没有足够的空间来存放";
		mes "更多的东西了. 您最好";
		mes "先去清理一下";
		mes "您的背包再来. ";
		close;
	}
	mes "[卡新]";
	mes "太棒了, 所有东西都准备";
	mes "齐全了, 让我们开始进行";
	mes "交易. ";
	mes "我保证您会对";
	mes "这些短刀的质量";
	mes "感到满意. ";
	delitem getarg(0),getarg(1)*.@amount;
	delitem getarg(2),getarg(3)*.@amount;
	getitem getarg(4),10*.@amount;
	close;
}
