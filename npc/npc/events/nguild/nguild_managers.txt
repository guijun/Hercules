//===== Hercules Script ======================================
//= War of Emperium Managers for N Guild Castles
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//=
//==============================================
//= Break down of arguments used in the function:
//=   arg(0): name of Castle Manager
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp to treasure room
//=   arg(3): y1 coordinate for warp to treasure room
//=   arg(4): guild script suffix for kafra, etc.
//===== Additional Comments: =================================
//= 1.0 Based off existing guild scripts.  Do not know if it is accurate.[kobra_k88]
//= 1.1 Added modified guild manager function. [L0ne_W0lf]
//= 	Removed guardian summoning.
//= 1.2 Corrected function name. (bugreport:1728) [L0ne_W0lf]
//============================================================

//==============================================
function	script	F_GldManager	{

	set @GID, getcastledata(getarg(1),1);
	mes "[ "+getarg(0)+" ]";
	if (@GID == 0){
		mes "我在等候我的主人. 勇敢的冒险家, 遵循您的命运!";
		return;
	}
	if (getcharid(2) != @GID){
		mes "我在此等候^5533FF" + getguildmaster(@GID) + "^000000的命令! 嘿! 你竟然不是工会的人!!";
		mes "守卫呢? 消灭入侵者!";
		return;
	}
	if (strcharinfo(0) != getguildmaster(@GID)){
		mes "你不是^5533FF" + getguildmaster(@GID) + "^000000! 我只听从^5533FF" + getguildmaster(@GID) + "^000000的命令!";
		return 0;
	}

	mes "欢迎您^5533FF" + getguildmaster(@GID) + "^000000! 我会竭尽所能为您效劳!";
	next;

	menu "卡普拉员工雇佣 / 解雇",M_Kaf, "进入宝藏室",M_Treas, "取消",M_End;

	//===========================
	M_Kaf:
	//======
		mes "[ "+getarg(0)+" ]";
		if (getcastledata(getarg(1),9) == 1) goto L_Dismiss;
		if (getgdskilllv(@GID,10001) == 0){
			mes "主人, 您还没有和卡普拉公司建立联系";
			mes "如果要雇佣卡普拉成员, 您必须首先学会^5533FF卡普拉契约^000000这项技能.";
			return;
		}

		L_Hire:
			mes "使用卡普拉服务吗? 您需要支付^5533FF10,000 Zeny^000000... ";
			next;
			menu "使用服务.",-,"取消",sM_KafEnd;

				mes "[ "+getarg(0)+" ]";
				if (Zeny < 10000){
					mes "主人, 您没有足够的金钱来支付服务费用.";
					return;
				}
				set Zeny,Zeny-10000;
				enablenpc "卡普拉员工#"+getarg(4);
				setcastledata getarg(1),9,1;
				mes "您已经和卡普拉公司建立了联系.";
				next;
				cutin "kafra_01",2;
				mes "[卡普拉员工]";
				mes "您好! 随时等候您的命令, 乐意为您效劳.";
				next;
				cutin "kafra_01",255;
				mes "[ "+getarg(0)+" ]";
				//mes "您的雇佣契约将会维持^5533FF30天^000000. 契约结束后, 即需要重新签定.";
				mes "我相信卡普拉员工会对工会有所帮助的.";
				return;

			sM_KafEnd:
				mes "[ "+getarg(0)+" ]";
				mes "遵命, 我的主人. 但是我建议尽快雇佣一名卡普拉员工!";
				return;

		L_Dismiss:
			mes "你想解雇目前的卡普拉员工吗?";
			next;
			menu "解雇",-,"取消",sM_KafEnd2;

				cutin "kafra_01",2;
				mes "[卡普拉成员]";
				mes "我做错什么事情了吗, 能原谅我吗, 主人?";
				next;
				menu "解散",-,"取消",ssM_KafEnd2;

					mes "[卡普拉成员]";
					mes "很不幸, 我不会为您的工会提供服务....";
					next;
					disablenpc "卡普拉员工#"+getarg(4);
					setcastledata getarg(1),9,0;
					cutin "kafra_01",255;
					mes "[ "+getarg(0)+" ]";
					mes "卡普拉成员解散了. 但是... 我们还是应该尽快雇佣一名卡普拉员工!";
					return;
				ssM_KafEnd2:
					mes "[卡普拉成员]";
					mes "谢谢您, 我的主人, 我会尽我所能!";
					cutin "kafra_01",255;
					return;
			sM_KafEnd2:
				mes "[ "+getarg(0)+" ]";
				mes "主人, 我想您应该保留目前的卡普拉成员, 因为她已经在全心为我们服务了.";
				return;

	//=========================
	M_Treas:
	//========
		mes "[ "+getarg(0)+" ]";
		mes "您要去我们的宝藏室吗? 只有您, 我的主人, 才能获准进入这个房间.";
		next;
		menu "进入宝藏室",-,"取消",sM_TresEnd;

			mes "[ "+getarg(0)+" ]";
			mes "请跟随我一起通过这条秘道.";
			mes "只有拉下这个秘密的开关才能出去.";
			next;
			warp getarg(1),getarg(2),getarg(3);
			return;
		sM_TresEnd:
			mes "[ "+getarg(0)+" ]";
			mes "每天都有货物产出.";
			mes "只要你需要, 最好随时取走. 因为如果时机不对, 它们会全部消失.";
			return;


	//==========================
	M_End:
	//=======
		mes "[ "+getarg(0)+" ]";
		mes "遵命, 主人.";
		return;
}

// Castle 1 ==================================================================================
nguild_alde,218,175,0	script	弗若罗	1_M_JOBTESTER,{
	callfunc "F_GldManager","弗若罗","nguild_alde",119,223,"N01";
	close;
}


// Castle 2 ==================================================================================
nguild_gef,40,48,5	script	雷珀	1_M_JOBTESTER,{
	callfunc "F_GldManager","雷珀","nguild_gef",155,112,"N02";
	close;
}


// Castle 3 ==================================================================================
nguild_pay,120,58,4	script	登达尔	1_M_JOBTESTER,{
	callfunc "F_GldManager","登达尔","nguild_pay",290,7,"N03";
	close;
}


// Castle 4 ==================================================================================
nguild_prt,112,181,0	script	弗腾	1_M_JOBTESTER,{
	callfunc "F_GldManager","弗腾","nguild_prt",15,209,"N04";
	close;
}
