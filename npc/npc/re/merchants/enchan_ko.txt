//===== RoSF.us Script =======================================
//= Kagerou/Oboro Gear Enchants
//===== By: =============Translation:=========================
//= Euphy                安赫尔[RoSF.us]
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds enchantments to Kagerou/Oboro gear.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//============================================================

que_ng,75,20,3	script	工匠拉登#ko	4_M_JPN2,{
	disable_items;
	mes "[工匠拉登]";
	mes "你好? 我是工匠拉登, 我是铁匠之中最受尊敬的.";
	next;
	mes "[工匠拉登]";
	mes "需要帮什么忙吗?";
	next;
	switch(select("- 您是哪位?:- 我想加点东西.:- 我想拆掉点东西.")) {
	case 1:
		mes "[工匠拉登]";
		mes "噢! 抱歉, 我还没有自我介绍.";
		next;
		mes "[工匠拉登]";
		mes "我从不会为了蝇头小利而动手.";
		next;
		mes "[工匠拉登]";
		mes "噢!你是不是想知道我能附魔那些武器和装备?";
		next;
		switch(select("- 看看我的武器清单吧.:- 看看我的防具清单吧.")) {
		case 1:
			mes "[工匠拉登]";
			mes "你对武器感兴趣吗?";
			next;
			mes "[工匠拉登]";
			mes "我可以操作的武器是 ";
			mes "^ff0000千年忍刀[1]^000000";
			mes "^ff0000三日月[1]^000000";
			mes "^ff0000风魔手里剑乱华[2]^000000";
			mes "^ff0000风魔手里剑雪月花^000000";
			mes "^ff0000风魔手里剑轰雷^000000";
			next;
			break;
		case 2:
			mes "[工匠拉登]";
			mes "你对防具感兴趣吗?";
			next;
			mes "[工匠拉登]";
			mes "我可以操作的防具是 ";
			mes "^ff0000狼纹笼手[1]^000000";
			mes "^ff0000月纹笼手[1]^000000";
			mes "^ff0000忍者鳞甲[1]^000000";
			mes "^ff0000虚影装束[1]^000000";
			mes "^ff0000特攻忍装[1]^000000";
			next;
			break;
		}
		mes "[工匠拉登]";
		mes "目前就是这些, 我也在不断学习新的武器与铠甲附魔改进技术.";
		next;
		mes "[工匠拉登]";
		mes "如果你愿意, 我可以以比较便宜的价格附魔改造你的第4和第3插槽.";
		close;
	case 2:
		mes "[工匠拉登]";
		mes "噢! 你需要改造一下你的装备么.";
		mes "我会为你的3/4插槽附魔一些强化实力的东西.";
		next;
		mes "[工匠拉登]";
		mes "由于我使用的材料加入了稳定而独特的方式, 所以你不必担心 ^ff0000'丢失精炼'^000000 或者 ^ff0000'损坏卡片'^000000.";
		next;
		mes "[工匠拉登]";
		mes "不过材料的成本非常高, 然而对于我的服务除材料费用仅索取 ^5565AA低廉的服务费^000000 .";
		next;
		mes "[工匠拉登]";
		mes "哈哈. 我想我太罗嗦了. 好了, 你想怎么操作?";
		next;
		switch(select("- 武器...:- 防具...")) {
		case 1:
			mes "[工匠拉登]";
			mes "你想附魔你的武器是吧. 我的服务费是 ^ff0000100,000 Zeny 1次.^000000";
			next;
			mes "[工匠拉登]";
			mes "^FF0000啊哈! 你需要把你要强化的武器装备在右手上, 这活对我来说非常容易.^000000";
			next;
			mes "[工匠拉登]";
			mes "选择你需要附魔的具体武器型号?";
			next;
			switch(select("- 慢着...:- 千年忍刀[1]:- 三日月[1]:- 风魔手里剑乱华[2]:- 风魔手里剑雪月花:- 风魔手里剑轰雷")) {
			case 1:
				mes "[工匠拉登]";
				mes "怎么改主意了? 好吧. 希望下次能见到你.";
				close;
			case 2:
				set .@equip_id,13076; //千年忍刀
				break;
			case 3:
				set .@equip_id,13078; //三日月
				break;
			case 4:
				set .@equip_id,13313; //风魔手里剑乱华
				break;
			case 5:
				set .@equip_id,13314; //风魔手里剑雪月花
				break;
			case 6:
				set .@equip_id,13315; //风魔手里剑轰雷
				break;
			}
			set .@part, EQI_HAND_R;
			break;
		case 2:
			mes "[工匠拉登]";
			mes "你想附魔你的防具是吧. 我的服务费是 ^ff0000100,000 Zeny 1次.^000000";
			next;
			mes "[工匠拉登]";
			mes "选择你需要附魔的具体防具型号?";
			next;
			switch(select("- 慢着...:- 狼纹笼手[1]:- 月纹笼手[1]:- 忍者鳞甲[1]:- 虚影装束[1]:- 特攻忍装[1]")) {
			case 1:
				mes "[工匠拉登]";
				mes "怎么改主意了? 好吧. 希望下次能见到你.";
				close;
			case 2:
				set .@part, EQI_HAND_L;
				set .@equip_id,2172; //狼纹笼手
				break;
			case 3:
				set .@part, EQI_HAND_L;
				set .@equip_id,2173; //月纹笼手
				break;
			case 4:
				set .@part, EQI_ARMOR;
				set .@equip_id,15054; //忍者鳞甲
				break;
			case 5:
				set .@part, EQI_ARMOR;
				set .@equip_id,15055; //虚影装束
				break;
			case 6:
				set .@part, EQI_ARMOR;
				set .@equip_id,15056; //特攻忍装
				break;
			}
			break;
		}
		set .@select,1;
		break;
	case 3:
		mes "[工匠拉登]";
		mes "你想清理下你的装备, 你是不是对现在的强化结果不满意?";
		next;
		mes "[工匠拉登]";
		mes "抱歉了可能我的制作经验还不太够. 你愿意付多少钱.";
		next;
		mes "[工匠拉登]";
		mes "^ff0000清理一个插槽 100,000 Zeny.^000000 你想清理哪件装备的附魔?";
		next;
		switch(select("- 慢着...:- 千年忍刀[1]:- 三日月[1]:- 风魔手里剑乱华[2]:- 风魔手里剑雪月花:- 风魔手里剑轰雷:- 狼纹笼手[1]:- 月纹笼手[1]:- 忍者鳞甲[1]:- 虚影装束[1]:- 特攻忍装[1]")) {
		case 1:
			mes "[工匠拉登]";
			mes "清理附魔的工作服务费是蛮贵的, 你觉得呢? 不好意思啊. 我会更加努力提升自己的技能并降低成本.";
			close;
		case 2:
			set .@part, EQI_HAND_R;
			set .@equip_id,13076; //千年忍刀
			break;
		case 3:
			set .@part, EQI_HAND_R;
			set .@equip_id,13078; //三日月
			break;
		case 4:
			set .@part, EQI_HAND_R;
			set .@equip_id,13313; //风魔手里剑乱华
			break;
		case 5:
			set .@part, EQI_HAND_R;
			set .@equip_id,13314; //风魔手里剑雪月花
			break;
		case 6:
			set .@part, EQI_HAND_R;
			set .@equip_id,13315; //风魔手里剑轰雷
			break;
		case 7:
			set .@part, EQI_HAND_L;
			set .@equip_id,2172; //狼纹笼手
			break;
		case 8:
			set .@part, EQI_HAND_L;
			set .@equip_id,2173; //月纹笼手
			break;
		case 9:
			set .@part, EQI_ARMOR;
			set .@equip_id,15054; //忍者鳞甲
			break;
		case 10:
			set .@part, EQI_ARMOR;
			set .@equip_id,15055; //虚影装束
			break;
		case 11:
			set .@part, EQI_ARMOR;
			set .@equip_id,15056; //特攻忍装
			break;
		}
		mes "[工匠拉登]";
		mes "你想重置 ^44B7BC" + getitemname(.@equip_id) + "^000000?";
		next;
		mes "[工匠拉登]";
		mes "费用是相同的, 但我不能提供该服务, 因为你要清理3号插槽的附魔前提是先清理4号插槽的附魔, 你是不是要这样做?";
		next;
		switch(select("- 清理第3插槽的附魔.:- 全部重置.")) {
		case 1:
			mes "[工匠拉登]";
			mes "很好. 我会开始清理第3插槽的附魔.";
			next;
			set .@sot03_ck,1;
			break;
		case 2:
			mes "[工匠拉登]";
			mes "很好. 我会重置所有插槽的附魔.";
			next;
			break;
		}
		set .@select,2;
		break;
	}
	if (getequipisequiped(.@part) == 0) {
		mes "[工匠拉登]";
		mes "在此之前你需要先装备 ^44B7BC" + getitemname(.@equip_id) + "^000000.";
		close;
	}
	if (Zeny < 100000) {  //custom translation
		mes "[工匠拉登]";
		mes "抱歉, 看上去你没带够钱!";
		next;
		mes "[工匠拉登]";
		mes "我们的收费是很低的, 所以请大家多多包涵.";
		close;
	}
	if (getequipid(.@part) != .@equip_id) {
		mes "[工匠拉登]";
		mes "我不认为这是你要操作的装备.";
		next;
		mes "[工匠拉登]";
		mes "我还无法对我提供的装备列表之外的装备进行附魔操作.";
		close;
	}

	set .@equip_refine, getequiprefinerycnt(.@part);
	setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);

	// Initialization
	if (.@select == 2) {
		if (.@sot03_ck) {
			if (.@equip_card[2] < 4700) { // Armor Enchant System
				mes "[工匠拉登]";
				mes "3号插槽本来就是空的. 请检查一下."; //custom translation
				close;
			}
			set .@equip_card[2],0;
		} else {
			if (.@equip_card[3] < 4700) { // Armor Enchant System
				mes "[工匠拉登]";
				mes "这件装备还没附魔过. 请再检查一下."; //custom translation
				close;
			}
			set .@equip_card[2],0;
			set .@equip_card[3],0;
		}
		progressbar "ffff00",2;
		Zeny -= 100000;
		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		mes "[工匠拉登]";
		mes "操作完成.";
		close;
	}

	// Enchantment
	     if (.@equip_refine <= 4)  set .@ko_type01,1;
	else if (.@equip_refine <= 7)  set .@ko_type01,2;
	else if (.@equip_refine <= 9)  set .@ko_type01,3;
	else if (.@equip_refine <= 12) set .@ko_type01,4;
	else                           set .@ko_type01,5;

	//custom translations
	if (.@equip_card[3] >= 4700) { // Armor Enchant System
		if (.@equip_card[2] >= 4700) {
			mes "[工匠拉登]";
			mes "这件装备无法再继续附魔了.";
			next;
			mes "[工匠拉登]";
			mes "如果你对现有的附魔不满意, 我建议你可以重置附魔.";
			close;
		} else {
			mes "[工匠拉登]";
			mes "4号插槽已经附魔过了. 是不是要开始附魔第3个插槽?";
			next;
			if(select("- 稍微等等:- 好的") == 1) {
				mes "[工匠拉登]";
				mes "好吧. 希望下次再见.";
				close;
			}
			mes "[工匠拉登]";
			mes "开始附魔3号插槽.";
			next;
			set .@ko_type02,2;
		}
	} else {
		mes "[工匠拉登]";
		mes "这件物品没有附魔过. 是不是要开始附魔?";
		next;
		if(select("- 稍微等等:- 好的") == 1) {
			mes "[工匠拉登]";
				mes "好吧. 希望下次再见.";
			close;
		}
		mes "[工匠拉登]";
		mes "开始附魔4号插槽.";
		next;
		set .@ko_type02,1;
	}
	if (.@ko_type02 == 1) {  // slot 4
		set .@r, rand(1,1000);
		switch (.@ko_type01) {
		case 1:
			     if (.@r <= 200) set .@e_socket04,4786; //Mdef2
			else if (.@r <= 350) set .@e_socket04,4787; //Mdef4
			else if (.@r <= 450) set .@e_socket04,4788; //Mdef6
			else if (.@r <= 500) set .@e_socket04,4789; //Mdef8
			else if (.@r <= 700) set .@e_socket04,4791; //Def3
			else if (.@r <= 850) set .@e_socket04,4792; //Def6
			else if (.@r <= 950) set .@e_socket04,4793; //Def9
			else                 set .@e_socket04,4794; //Def12
			set .@enchant, .@e_socket04;
			break;
		case 2:
			     if (.@r <= 90)  set .@d_socket04,4786; //Mdef2
			else if (.@r <= 170) set .@d_socket04,4787; //Mdef4
			else if (.@r <= 240) set .@d_socket04,4788; //Mdef6
			else if (.@r <= 300) set .@d_socket04,4789; //Mdef8
			else if (.@r <= 390) set .@d_socket04,4791; //Def3
			else if (.@r <= 470) set .@d_socket04,4792; //Def6
			else if (.@r <= 540) set .@d_socket04,4793; //Def9
			else if (.@r <= 600) set .@d_socket04,4794; //Def12
			else if (.@r <= 660) set .@d_socket04,4700; //Strength1
			else if (.@r <= 720) set .@d_socket04,4710; //Inteligence1
			else if (.@r <= 780) set .@d_socket04,4740; //Vitality1
			else if (.@r <= 840) set .@d_socket04,4720; //Dexterity1
			else if (.@r <= 900) set .@d_socket04,4730; //Agility1
			else                 set .@d_socket04,4750; //Luck1
			set .@enchant, .@d_socket04;
			break;
		case 3:
			     if (.@r <= 90)  set .@c_socket04,4700; //Strength1
			else if (.@r <= 180) set .@c_socket04,4710; //Inteligence1
			else if (.@r <= 270) set .@c_socket04,4740; //Vitality1
			else if (.@r <= 360) set .@c_socket04,4720; //Dexterity1
			else if (.@r <= 450) set .@c_socket04,4730; //Agility1
			else if (.@r <= 600) set .@c_socket04,4750; //Luck1
			else if (.@r <= 660) set .@c_socket04,4701; //Strength2
			else if (.@r <= 720) set .@c_socket04,4711; //Inteligence2
			else if (.@r <= 780) set .@c_socket04,4741; //Vitality2
			else if (.@r <= 840) set .@c_socket04,4721; //Dexterity2
			else if (.@r <= 900) set .@c_socket04,4731; //Agility2
			else                 set .@c_socket04,4751; //Luck2
			set .@enchant, .@c_socket04;
			break;
		case 4:
			     if (.@r <= 70)  set .@b_socket04,4700; //Strength1
			else if (.@r <= 140) set .@b_socket04,4710; //Inteligence1
			else if (.@r <= 210) set .@b_socket04,4740; //Vitality1
			else if (.@r <= 280) set .@b_socket04,4720; //Dexterity1
			else if (.@r <= 350) set .@b_socket04,4730; //Agility1
			else if (.@r <= 440) set .@b_socket04,4750; //Luck1
			else if (.@r <= 495) set .@b_socket04,4701; //Strength2
			else if (.@r <= 550) set .@b_socket04,4711; //Inteligence2
			else if (.@r <= 605) set .@b_socket04,4741; //Vitality2
			else if (.@r <= 660) set .@b_socket04,4721; //Dexterity2
			else if (.@r <= 715) set .@b_socket04,4731; //Agility2
			else if (.@r <= 800) set .@b_socket04,4751; //Luck2
			else if (.@r <= 860) set .@b_socket04,4795; //HP100
			else if (.@r <= 910) set .@b_socket04,4796; //HP200
			else if (.@r <= 950) set .@b_socket04,4797; //HP300
			else if (.@r <= 980) set .@b_socket04,4800; //SP50
			else                 set .@b_socket04,4801; //SP100
			set .@enchant, .@b_socket04;
			break;
		case 5:
		         if (.@r <= 59)  set .@a_socket04,4701; //Strength2
			else if (.@r <= 118) set .@a_socket04,4711; //Inteligence2
			else if (.@r <= 177) set .@a_socket04,4741; //Vitality2
			else if (.@r <= 236) set .@a_socket04,4721; //Dexterity2
			else if (.@r <= 295) set .@a_socket04,4731; //Agility2
			else if (.@r <= 354) set .@a_socket04,4751; //Luck2
			else if (.@r <= 413) set .@a_socket04,4795; //HP100
			else if (.@r <= 472) set .@a_socket04,4796; //HP200
			else if (.@r <= 530) set .@a_socket04,4797; //HP300
			else if (.@r <= 589) set .@a_socket04,4800; //SP50
			else if (.@r <= 648) set .@a_socket04,4801; //SP100
			else if (.@r <= 707) set .@a_socket04,4702; //Strength3
			else if (.@r <= 765) set .@a_socket04,4712; //Inteligence3
			else if (.@r <= 824) set .@a_socket04,4742; //Vitality3
			else if (.@r <= 883) set .@a_socket04,4722; //Dexterity3
			else if (.@r <= 942) set .@a_socket04,4732; //Agility3
			else                 set .@a_socket04,4752; //Luck3
			set .@enchant, .@a_socket04;
			break;
		}
	} else if (.@ko_type02 == 2) {  // slot 3
		set .@r, rand(1,1000);
		switch (.@ko_type01) {
		case 1:
			     if (.@r <= 125) set .@e_socket03,4786; //Mdef2
			else if (.@r <= 250) set .@e_socket03,4787; //Mdef4
			else if (.@r <= 375) set .@e_socket03,4788; //Mdef6
			else if (.@r <= 500) set .@e_socket03,4789; //Mdef8
			else if (.@r <= 625) set .@e_socket03,4791; //Def3
			else if (.@r <= 750) set .@e_socket03,4792; //Def6
			else if (.@r <= 875) set .@e_socket03,4793; //Def9
			else                 set .@e_socket03,4794; //Def12
			set .@enchant, .@e_socket03;
			break;
		case 2:
			      if (.@r <= 100) set .@d_socket03,4786; //Mdef2
			else if (.@r <= 190) set .@d_socket03,4787; //Mdef4
			else if (.@r <= 270) set .@d_socket03,4788; //Mdef6
			else if (.@r <= 340) set .@d_socket03,4789; //Mdef8
			else if (.@r <= 440) set .@d_socket03,4791; //Def3
			else if (.@r <= 530) set .@d_socket03,4792; //Def6
			else if (.@r <= 610) set .@d_socket03,4793; //Def9
			else if (.@r <= 680) set .@d_socket03,4794; //Def12
			else if (.@r <= 720) set .@d_socket03,4700; //Strength1
			else if (.@r <= 760) set .@d_socket03,4710; //Inteligence1
			else if (.@r <= 800) set .@d_socket03,4740; //Vitality1
			else if (.@r <= 850) set .@d_socket03,4720; //Dexterity1
			else if (.@r <= 880) set .@d_socket03,4730; //Agility1
			else                 set .@d_socket03,4750; //Luck1
			set .@enchant, .@d_socket03;
			break;
		case 3:
			     if (.@r <= 100) set .@c_socket03,4700; //Strength1
			else if (.@r <= 200) set .@c_socket03,4710; //Inteligence1
			else if (.@r <= 305) set .@c_socket03,4740; //Vitality1
			else if (.@r <= 410) set .@c_socket03,4720; //Dexterity1
			else if (.@r <= 500) set .@c_socket03,4730; //Agility1
			else if (.@r <= 700) set .@c_socket03,4750; //Luck1
			else if (.@r <= 750) set .@c_socket03,4701; //Strength2
			else if (.@r <= 780) set .@c_socket03,4711; //Inteligence2
			else if (.@r <= 830) set .@c_socket03,4741; //Vitality2
			else if (.@r <= 880) set .@c_socket03,4721; //Dexterity2
			else if (.@r <= 900) set .@c_socket03,4731; //Agility2
			else                 set .@c_socket03,4751; //Luck2
			set .@enchant, .@c_socket03;
			break;
		case 4:
			     if (.@r <= 70)  set .@b_socket03,4700; //Strength1
			else if (.@r <= 160) set .@b_socket03,4710; //Inteligence1
			else if (.@r <= 250) set .@b_socket03,4740; //Vitality1
			else if (.@r <= 340) set .@b_socket03,4720; //Dexterity1
			else if (.@r <= 400) set .@b_socket03,4730; //Agility1
			else if (.@r <= 500) set .@b_socket03,4750; //Luck1
			else if (.@r <= 540) set .@b_socket03,4701; //Strength2
			else if (.@r <= 580) set .@b_socket03,4711; //Inteligence2
			else if (.@r <= 625) set .@b_socket03,4741; //Vitality2
			else if (.@r <= 670) set .@b_socket03,4721; //Dexterity2
			else if (.@r <= 700) set .@b_socket03,4731; //Agility2
			else if (.@r <= 800) set .@b_socket03,4751; //Luck2
			else if (.@r <= 860) set .@b_socket03,4795; //HP100
			else if (.@r <= 900) set .@b_socket03,4796; //HP200
			else if (.@r <= 920) set .@b_socket03,4797; //HP300
			else if (.@r <= 965) set .@b_socket03,4800; //SP50
			else                 set .@b_socket03,4801; //SP100
			set .@enchant, .@b_socket03;
			break;
		case 5:
			     if (.@r <= 59)  set .@a_socket03,4701; //Strength2
			else if (.@r <= 118) set .@a_socket03,4711; //Inteligence2
			else if (.@r <= 177) set .@a_socket03,4741; //Vitality2
			else if (.@r <= 236) set .@a_socket03,4721; //Dexterity2
			else if (.@r <= 295) set .@a_socket03,4731; //Agility2
			else if (.@r <= 354) set .@a_socket03,4751; //Luck2
			else if (.@r <= 413) set .@a_socket03,4795; //HP100
			else if (.@r <= 472) set .@a_socket03,4796; //HP200
			else if (.@r <= 530) set .@a_socket03,4797; //HP300
			else if (.@r <= 589) set .@a_socket03,4800; //SP50
			else if (.@r <= 648) set .@a_socket03,4801; //SP100
			else if (.@r <= 707) set .@a_socket03,4702; //Strength3
			else if (.@r <= 765) set .@a_socket03,4712; //Inteligence3
			else if (.@r <= 824) set .@a_socket03,4742; //Vitality3
			else if (.@r <= 883) set .@a_socket03,4722; //Dexterity3
			else if (.@r <= 942) set .@a_socket03,4732; //Agility3
			else                 set .@a_socket03,4752; //Luck3
			set .@enchant, .@a_socket03;
			break;
		}
	} else {
		mes "[工匠拉登]";
		mes "发生一个未知错误. 请联系GM. *^_^*";
		close;
	}
	if (.@ko_type02 == 1) {
		set .@equip_card[2], .@equip_card[3];
		set .@equip_card[3], .@enchant;
	} else
		set .@equip_card[2], .@enchant;
	progressbar "ffff00",2;
	Zeny -= 100000;
	delequip .@part;

//	GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
	getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

	mes "[工匠拉登]";
	mes "^44B7BC" + getitemname(.@equip_id) + "^000000";
	mes "装备已经附魔完成."; //custom translation
	close;
}
