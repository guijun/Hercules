//===== Hercules Script ======================================
//= 3rd Item Seller
//===== By: ==================================================
//= Masao, Mercurial
//===== Current Version: =====================================
//= 1.5
//===== Description: ========================================= 
//= [Official Conversion]
//= Sells some 3rd Job related items:
//= - Poison Herbs, Rune Stones, Rare Herbs, Points
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Optimized Poison Herb Salesman NPC
//= 1.2 Optimized and standardized. [Euphy]
//= 1.3 Added Malangdo/Mora Points NPC. [Euphy]
//= 1.4 Updated to match the official scripts. [Euphy]
//= 1.5 Added Izlude duplicates. [Euphy]
//============================================================ 

// Poison Herb Merchants (Guillotine Cross)
//============================================================
-	script	::phs	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 请稍等 !! -";
		mes "- 当前您所携带的物品 -";
		mes "- 种类过于繁多或者超过负重. -";
		mes "- 请稍做处理后 -";
		mes "- 再进行交易. -";
		close;
	}
	mes "[毒药药草商人]";
	mes "我是专业的";
	mes "毒药药草商人.";
	mes "每个药草的售价 4,000 zeny.";
	mes "您想要买哪个?";
	next;
	set .@i, select("- 夹竹桃:- 马缨丹:- 斑叶竹:- 蛇足草:- 赛莨菪:- 透骨草:- 制毒工具箱:- 取消")-1;
	mes "[毒药药草商人]";
	if (.@i < 6) {
		set .@item,7932+.@i;
		set .@price,4000;
		set .@max,2000;
		set .@max$,"2,000";
	} else if (.@i == 6) {
		set .@item,7931;
		set .@price,5000;
		set .@max,500;
		set .@max$,"500";
		mes "每个制毒工具箱费用 5,000 zeny.";
	} else {
		mes "好吧, 我明白了. 请随时惠顾~!";
		close;
	}
	mes "您需要购买多少个?";
	mes "您最大可以购买 '"+.@max+"' 个.";
	mes "如果您想取消, 请按  '0'.";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[毒药药草商人]";
		mes "您已经取消交易.";
		close;
	}
	if (.@amount > .@max) {
		mes "[毒药药草商人]";
		mes "数量必须小于 "+.@max$+"!";
		close;
	}
	set .@total, .@amount * .@price;
	if (Zeny < .@total) {
		mes "[毒药药草商人]";
		mes "您的钱不够.";
		mes "请先检查您所携带的Zeny.";
		close;
	}
	set Zeny, Zeny - .@total;
	getitem .@item, .@amount;
	mes "[毒药药草商人]";
	mes "谢谢. 欢迎下次再来~!";
	close;
}
job3_guil01,79,96,3	duplicate(phs)	毒药药草商人	4_F_YUNYANG
morocc,191,94,4	duplicate(phs)	毒药药草商人#moc	4_F_YUNYANG	//Official: (190,96) -> in a wall
lhz_in02,16,205,4	duplicate(phs)	毒药药草商人#lhz	4_F_YUNYANG

// Rune Stone Merchants (Rune Knight)
//============================================================
-	script	::runesale	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 请稍等 !! -";
		mes "- 当前您所携带的物品 -";
		mes "- 种类过于繁多或者超过负重. -";
		mes "- 请稍做处理后 -";
		mes "- 再进行交易. -";
		close;
	}
	mes "[符文商人]";
	mes "嘿, 您需要符文吗?!";
	mes "我出售各种符文石!";
	mes "您喜欢哪个?";
	next;
	set .@i, select("- 购买高级符文原石.:- 购买普通符文原石.");
	mes "[符文商人]";
	if (.@i == 1) {
		mes "高级符文原石!";
		mes "需要 2,500 Zeny 每个!";
		set .@item,12734; //Runstone_Quality
		set .@price,2500;
	} else {
		mes "普通符文原石?";
		mes "需要 1,000 zeny 每个!";
		set .@item,12737; //Runstone_Ordinary
		set .@price,1000;
		setarray .@rune[0],12737,1000;
	}
	mes "告诉我您需要多少个,";
	mes "但记住, 您最多";
	mes "只能购买 '2000' 个.";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[符文商人]";
		mes "您不买吗? 请走开!";
		close;
	}
	if (.@amount > 2000) {
		mes "[符文商人]";
		mes "数量必须小于 2,000!";
		close;
	}
	set .@total, .@amount * .@price;
	if (Zeny < .@total) {
		mes "[符文商人]";
		mes "Zeny! Zeny!";
		mes "您的钱不够.";
		mes "请先检查下您有多少钱.";
		close;
	}
	if (!checkweight(.@item,.@amount)) {
		mes "[符文商人]";
		mes "看上去您应该拿不了呢么多, 请再次尝试一下?!";
		close;
	}
	set Zeny, Zeny - .@total;
	getitem .@item, .@amount;
	mes "[符文商人]";
	mes "谢谢.";
	mes "请下次再来~!";
	close;
}
job3_rune01,90,62,3	duplicate(runesale)	符文商人#job3	4_M_YURI
prontera,168,228,3	duplicate(runesale)	符文商人#prt	4_M_YURI

// Rare Herb Merchants (Guillotine Cross)
//============================================================
job3_guil01,91,93,3	script	稀有草药收集者	1_M_03,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 请稍等 !! -";
		mes "- 当前您所携带的物品 -";
		mes "- 种类过于繁多或者超过负重. -";
		mes "- 请稍做处理后 -";
		mes "- 再进行交易. -";
		close;
	}
	if (Class == Job_Guillotine_Cross || Class == Job_Guillotine_Cross_T || Class == Job_Baby_Cross) {
		mes "[稀有草药收集者]";
		mes "我徘徊在世界各地收集罕见的毒药药材. 最近, 我开始处理药草称为Izidor. 如果您有兴趣, 您可以购买它.";
		next;
		set .@i, select("- 我应该如何购买?:- 用家畜血来交换:- 用苦味草来交换:- 用青嫩毒草来交换:- 用冰雕玫瑰来交换:- 用曼特疗伤药来交换:- 用西纳雷花来交换")-2;
		if (.@i == -1) {
			mes "[稀有草药收集者]";
			mes "您可以花 10,000 Zeny 购买他们, 同时还需要, 家畜血, 苦味草, 青嫩毒草, 冰雕玫瑰, 曼特疗伤药, 西纳雷花之一.";
			next;
			mes "[稀有草药收集者]";
			mes "为什么我问钱? 这只是付出一点对于一个像我这样的收藏家. 哈哈哈...";
			close;
		} else {
			setarray .@exchange[0],702,621,631,749,605,703;
			set .@item, .@exchange[.@i];
			set .@price, 10000;

			mes "[稀有草药收集者]";
			mes "您要多少?";
			mes "您只能购买最多 '2000' 个.";
			mes "如果您想取消, 请按  '0'.";
			next;
			input .@amount;
			if (.@amount == 0) {
				mes "[稀有草药收集者]";
				mes "您已经取消交易.";
				close;
			}
			if (.@amount > 2000) {
				mes "[稀有草药收集者]";
				mes "数量必须小于 2,000!";
				close;
			}
			set .@total, .@amount * .@price;
			if (countitem(.@item) < .@amount || Zeny < .@total) {
				mes "[稀有草药收集者]";
				mes "等您装备好所有的东西我们再进行交易吧.";
				close;
			}
			mes "[稀有草药收集者]";
			mes "我收钱和特殊物品.";
			delitem .@item, .@amount;
			set Zeny, Zeny - .@total;
			getitem 709, .@amount; //Izidor
			close;
		}
	}
	mes "[稀有草药收集者]";
	mes "我徘徊在世界各地收集罕见的毒药药材. 但我想卖给像您这样的人...哈哈哈...";
	close;
}

// Point Merchants (Sorcerer)
//============================================================
-	script	::pss	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 请稍等 !! -";
		mes "- 当前您所携带的物品 -";
		mes "- 种类过于繁多或者超过负重. -";
		mes "- 请稍做处理后 -";
		mes "- 再进行交易. -";
		close;
	}
	mes "[精华商人]";
	mes "您好. 我卖元素使的催化剂被称为 ^FF0000精华^000000. 您需要什么?";
	next;
	set .@i, select("- 火灵碎片 - 200z:- 地灵精华 - 200z:- 水灵碎片 - 200z:- 风灵精华 - 200z:- 取消")-1;
	mes "[精华商人]";
	if (.@i == 4) {
		mes "没有您需要的东西吗?";
		close;
	}
	setarray .@itemid[0],6360,6363,6361,6362;
	setarray .@color$[0],"火灵","地灵","水灵","风灵";
	mes "您选择了 "+.@color$[.@i]+" 精华.";
	mes "您需要购买多少个?";
	mes "如果您想取消, 请按 '0'.";
	next;
	input .@amount;
	mes "[精华商人]";
	if (.@amount == 0) {
		mes "您已经取消交易.";
		close;
	}
	if (Zeny < .@amount*200) {
		mes "您的钱不够.";
		close;
	}
	if (!checkweight(.@itemid[.@i],.@amount)) {
		mes "您没有足够的空间来存放您购买的各类精华.";
		close;
	}
	Zeny -= (.@amount*200);
	getitem .@itemid[.@i],.@amount;
	mes "非常感谢, 再见~!";
	close;
}
gef_tower,105,172,5	duplicate(pss)	精华商人#Sorcerer	8_F_GIRL
prt_in,131,66,0	duplicate(pss)	精华商人#Prontera	8_F_GIRL
alberta,105,52,7	duplicate(pss)	精华商人#Alberta	8_F_GIRL
aldebaran,133,114,5	duplicate(pss)	精华商人#Aldebaran	8_F_GIRL
comodo,193,159,5	duplicate(pss)	精华商人#Comodo	8_F_GIRL
geffen,129,49,5	duplicate(pss)	精华商人#Geffen	8_F_GIRL
izlude,138,163,5	duplicate(pss)	精华商人#Izlude	8_F_GIRL	// Old coordinates: izlude (135,121)
izlude_a,138,163,5	duplicate(pss)	精华商人#Izlude_a	8_F_GIRL
izlude_b,138,163,5	duplicate(pss)	精华商人#Izlude_b	8_F_GIRL
izlude_c,138,163,5	duplicate(pss)	精华商人#Izlude_c	8_F_GIRL
izlude_d,138,163,5	duplicate(pss)	精华商人#Izlude_d	8_F_GIRL
malangdo,214,163,5	duplicate(pss)	精华商人#Malangdo	8_F_GIRL
mora,115,118,3	duplicate(pss)	精华商人#Mora	8_F_GIRL
ra_in01,256,273,3	duplicate(pss)	精华商人#Rachel	8_F_GIRL
veins,202,128,5	duplicate(pss)	精华商人#Veins	8_F_GIRL
dicastes01,207,200,5	duplicate(pss)	精华商人#Dicastes	8_F_GIRL
manuk,261,206,3	duplicate(pss)	精华商人#Manuk	8_F_GIRL
splendide,207,160,5	duplicate(pss)	精华商人#Splendide	8_F_GIRL
mid_camp,224,237,3	duplicate(pss)	精华商人#Midgard	8_F_GIRL
