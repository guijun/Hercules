//===== Hercules Script ======================================
//= Job Repairs
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= [Official Conversion]
//= Removes potentially conflicting job data.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Added GM management function. [Euphy]
//============================================================

/*
rAthena -> official job variable conversion
-------------------------------------------

Knight: KNIGHT_Q -> job_knight_q
Priest: PRIEST_Q -> job_priest_q
Wizard: WIZ_Q -> job_wizard_q
Blacksmith: BSMITH_Q -> job_black
Assassin: ASSIN_Q -> job_assasin
Hunter: HNTR_Q -> job_hunter
Crusader: CRUS_Q -> job_crusader_q
Monk: MONK_Q -> job_monk
Sage: SAGE_Q -> job_sage_q
Alchemist: ALCH_Q -> job_alchemist_q
Rogue: ROGUE_Q -> job_rogue
Bard: BARD_Q -> job_bard_q
Dancer: DANC_Q -> job_dancer

Rebirth: ADVJOB -> job_karma

Rune Knight: job_rune_edq
Arch Bishop: job_arch
Warlock: job_wl
Mechanic: job__mechanic -> job_mechanic
Guillotine Cross: job_3rd_gc
Ranger: job_ranger01
Royal Guard: job_royal -> job_roy
Sura: job_shu
Sorcerer: job_soc
Geneticist: job_gen
Shadow Chaser: job_sha -> job_3rd_sc
Minstrel: job_min
Wanderer: job_wan

Gunslinger: GUNS_Q -> hg_gun
Ninja: NINJ_Q -> hg_nin
Taekwon: TK_Q -> job_taekwon
Star Gladiator: STGL_Q -> job_star
Soul Linker: SOUL_Q -> job_linker
Super Novice: SUPNOV_Q -> job_super
Expanded Super Novice: EXSUPNOV_Q -> job_super2
*/

prt_in,38,104,4	script	瓦莱丽	4_F_VALKYRIE,{
if(getgmlevel() == 99){
	mes "[瓦莱丽]";
	mes "GM您好, 您可以选择进入";
	mes "^FF0000管理菜单[GM]^000000";
	if(select("- 不用!:- 是的, 我要进入GM管理程序.") == 2) {
		mes "[瓦莱丽]";
		mes "请稍等.";
		callfunc "F_GM_NPC";
		close;
	}
}
	if (checkweight(1201,1) == 0) {
		mes "物品过多, 请稍候再来.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "负重不够, 请稍候再来.";
		close;
	}
	mes "[瓦莱丽]";
	mes "我是瓦莱丽, 我必须帮助那些状态异常的人. 我是巴基力的朋友.";
	next;
	mes "[瓦莱丽]";
	mes "如果双方的异常状态都消失, 那么我们就必须从头开始职业测试. 这样你还愿意吗?";
	next;
	switch(select("- 我没有任何异常:- 我想我有一些异常")) {
	case 1:
		mes "[瓦莱丽]";
		mes "那不错. 请保重.";
		close;
	case 2:
		mes "[瓦莱丽]";
		mes "你决定好了要重置状态吗?";
		next;
		if(select("- 不:- 是的, 我决定了.") == 1) {
			mes "[瓦莱丽]";
			mes "明白了. 再见.";
			close;
		}
		mes "[瓦莱丽]";
		mes "所有与任务相关的物品, 在现有的情况下都会消失. 你觉得没问题吗?";
		next;
		if(select("- 不:- 是的, 我决定了.") == 1) {
			mes "[瓦莱丽]";
			mes "明白了. 再见.";
			close;
		}
		mes "[瓦莱丽]";
		mes "貌似有很多人因为无法获得和其他人相同的晋级机会而被世界所遗忘, 即使这样你也愿意吗? ";
		next;
		if(select("- 不:- 是的, 我决定了.") == 1) {
			mes "[瓦莱丽]";
			mes "明白了. 再见.";
			close;
		}
		mes "[瓦莱丽]";
		mes "在这种情况下, 需要先做一些处理, 你确定重置了吗?";
		next;
		if(select("- 不:- 是的, 我决定了.") == 1) {
			mes "[瓦莱丽]";
			mes "明白了. 再见.";
			close;
		}
		specialeffect2 EF_INVENOM;
		progressbar "0xffff00",3;

		// Warlock - quest items
		if (isequipped(2796) || isequipped(2797)) {
			mes "[瓦莱丽]";
			mes "请脱下你的魔法戒指.";
			close;
		}
		if (countitem(2796) || countitem(2797)) {
			if (isequipped(2796) || isequipped(2797)) {
				mes "[瓦莱丽]";
				mes "请脱下戒指后和我对话.";
				close;
			}
			setarray .@items[0], 2796, 2797, 6152, 6151; //Magical_Stone,Magical_Stone_,Glittering_Crystal,Peice_Of_Great_Bradium
			for(set .@i,0; .@i<4; set .@i,.@i+1) {
				if (countitem(.@items[.@i]))
					delitem .@items[.@i], countitem(.@items[.@i]);
			}
			mes "[瓦莱丽]";
			mes "你佩戴的一些物品隔绝了我的魔力. 我会帮你清理掉.";
			close;
		}

		// Baby and third job characters meet no further conditions.
		if (eaclass()&(EAJL_BABY|EAJL_THIRD)) {
			mes "[瓦莱丽]";
			mes "我觉得我们应该没有任何的交集.";
			close;
		}

		// Rebirth - variable
		if ((Class >= Job_Swordman && Class <= Job_Crusader2) && ADVJOB != 0) {
			set ADVJOB,0;
			mes "[瓦莱丽]";
			mes "你身上有着巨大的异常状态- 你想处理吗.";
			next;
			mes "[瓦莱丽]";
			mes "如果你觉得有额外问题的话, 可以在此与我对话.";
			close;
		}

		// Second job - variable and quests
		// callsub L_Second, "<variable>", <quest>;
		if (Class == Job_Swordman) {
			callsub L_Second, "KNIGHT_Q", 9012;
			callsub L_Second, "CRUS_Q", 3015;
		} else if (Class == Job_Archer) {
			callsub L_Second, "HNTR_Q", 4013;
			callsub L_Second, "BARD_Q", 3004;
			callsub L_Second, "DANC_Q", 7006;
		} else if (Class == Job_Mage) {
			callsub L_Second, "WIZ_Q", 9018;
			callsub L_Second, "SAGE_Q", 2052;
		} else if (Class == Job_Merchant) {
			callsub L_Second, "BSMITH_Q", 2016;
			callsub L_Second, "ALCH_Q", 2040;
		} else if (Class == Job_Thief) {
			callsub L_Second, "ASSIN_Q", 8008;
			callsub L_Second, "ROGUE_Q", 2027;
		} else if (Class == Job_Acolyte) {
			callsub L_Second, "PRIEST_Q", 8016;
			callsub L_Second, "MONK_Q", 3032;
		}

		// Third job - variable and quests
		// callsub L_Third, "<variable>", <main quest>, <first quest>, <last quest>{, <additional quest>};
		if (BaseJob == Job_Assassin)
			callsub L_Third, "job_3rd_gc", 7100, 7091, 7116;
		else if (BaseJob == Job_Blacksmith)
			callsub L_Third, "job__mechanic", 10101, 10090, 10101;
		else if (BaseJob == Job_Hunter)
			callsub L_Third, "job_ranger01", 8262, 8254, 8262;
		else if (BaseJob == Job_Knight)
			callsub L_Third, "job_rune_edq", 3219, 3200, 3220;
		else if (BaseJob == Job_Priest)
			callsub L_Third, "job_arch", 2191, 2187, 2191;
		else if (BaseJob == Job_Wizard)
			callsub L_Third, "job_wl", 11112, 11106, 11112;
		else if (BaseJob == Job_Alchemist)
			callsub L_Third, "job_gen", 2223, 2209, 2217, 2223;
		else if (BaseJob == Job_Bard)
			callsub L_Third, "job_min", 11154, 11135, 11154;
		else if (BaseJob == Job_Crusader)
			callsub L_Third, "job_royal", 0, 12090, 12094;
		else if (BaseJob == Job_Dancer)
			callsub L_Third, "job_wan", 2222, 2218, 2222;
		else if (BaseJob == Job_Monk)
			callsub L_Third, "job_shu", 11158, 11155, 11158;
		else if (BaseJob == Job_Rogue)
			callsub L_Third, "job_sha", 7180, 7160, 7180;
		else if (BaseJob == Job_Sage)
			callsub L_Third, "job_soc", 12098, 12096, 12098;

		mes "[瓦莱丽]";
		mes "我觉得我们应该没有任何的交集.";
		close;
	}

L_Second:
	if (getd(getarg(0)) != 0) {
		setd getarg(0),0;
		if (checkquest(getarg(1)) == 2)
			erasequest getarg(1);
		mes "[瓦莱丽]";
		mes "我会帮助清除你的问题, 就如同之前得到我帮助的人一样.";
		next;
		mes "[瓦莱丽]";
		mes "如果你觉得有额外问题的话, 可以在此与我对话.";
		close;
	} else
		return;

L_Third:
	if (getd(getarg(0)) != 0) {
		setd getarg(0),0;
		if (getarg(1)) {
			if (checkquest(getarg(1)) == 2)
				erasequest getarg(1);
		}
		for(set .@i,getarg(2); .@i<=getarg(3); set .@i,.@i+1) {
			if (checkquest(.@i) > -1)
				erasequest .@i;
		}
		if (getarg(4,0)) {
			if (checkquest(getarg(4)) > -1)
				erasequest getarg(4);
		}
		mes "[瓦莱丽]";
		mes "我会帮助清除你的问题, 就如同之前得到我帮助的人一样.";
		next;
		mes "[瓦莱丽]";
		mes "如果你觉得有额外问题的话, 可以在此与我对话.";
		close;
	} else
		return;
}

-	script	瓦莱丽#sign	-1,{
	mes "^ff00ff我无法支付租金, 所以我搬迁去了普隆德拉水池附近.^000000";
	mes " ";
	mes "^ff00ff瓦莱丽^000000";
	close;
}
in_moc_16,17,33,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#asn	2_BULLETIN_BOARD
ein_in01,13,28,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#bls	2_BULLETIN_BOARD
hu_in01,386,377,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#hnt	2_BULLETIN_BOARD
prt_in,88,108,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#knt	2_BULLETIN_BOARD
prt_church,19,36,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#prst	2_BULLETIN_BOARD
gef_tower,110,30,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#wiz	2_BULLETIN_BOARD
comodo,215,155,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#danc	2_BULLETIN_BOARD
prt_castle,45,167,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#cru	2_BULLETIN_BOARD
prt_monk,59,253,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#mnk	2_BULLETIN_BOARD
moc_ruins,90,104,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#rg	2_BULLETIN_BOARD
yuno_in02,43,62,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#sag	2_BULLETIN_BOARD
prt_church,103,86,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#arch	2_BULLETIN_BOARD
que_job01,75,92,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#gc	2_BULLETIN_BOARD
yuno,129,150,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#mech	2_BULLETIN_BOARD
tur_dun01,159,40,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#ran	2_BULLETIN_BOARD
prt_in,167,24,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#rk	2_BULLETIN_BOARD
spl_in02,81,102,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#wl	2_BULLETIN_BOARD
alde_alche,38,181,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#alche	2_BULLETIN_BOARD
alberta,198,129,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#min	2_BULLETIN_BOARD
in_rogue,376,104,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#sc	2_BULLETIN_BOARD
ve_in,244,122,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#shu	2_BULLETIN_BOARD
xmas,166,209,4	duplicate(瓦莱丽#sign)	瓦莱丽的告示#wan	2_BULLETIN_BOARD

sec_in02,12,40,5	script	职业修复	1_M_LIBRARYMASTER,1,1,{
	callfunc "F_GM_NPC";
	mes "请输入密码: 1854.";
	next;
	if (callfunc("F_GM_NPC","1854",1) == 1) {
		set job_arch,100;
		set job_3rd_gc,100;
		set job__mechanic,100;
		set job_ranger01,100;
		set job_rune_edq,100;
		set job_wl,100;
		set job_gen,100;
		set job_min,100;
		set job_royal,100;
		set job_sha,100;
		set job_soc,100;
		set job_shu,100;
		set job_wan,100;
		set KNIGHT_Q,100;
		set PRIEST_Q,100;
		set WIZ_Q,100;
		set BSMITH_Q,100;
		set ASSIN_Q,100;
		set HNTR_Q,100;
		set CRUS_Q,100;
		set MONK_Q,100;
		set SAGE_Q,100;
		set ALCH_Q,100;
		set ROGUE_Q,100;
		set BARD_Q,100;
		set DANC_Q,100;
		set ADVJOB,15;
	}
	close;
}
