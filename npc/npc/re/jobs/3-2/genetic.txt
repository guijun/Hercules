//===== RoSF.us Script ======================================
//= Genetic Job Quest
//===== By: ==================================================
//= Muad_Dib, Aeomin
//===== Current Version: =====================================
//= 1.3a
//===== Description: =========================================
//= [Official Conversion]
//= Job change Quest from Alchemist / Creator -> Genetic.
//===== Additional Comments: =================================
//= 1.0 First Version. [Masao]
//= 1.1 Fixed Scatter Stuff NPCs [JayPee].
//= 1.2 Updated dialogue. Credits to Muad_Dib. [Joseph]
//= 1.3 Updated script, optimized. [Euphy]
//= 1.3a Moved warps to separate file. [Euphy]
//============================================================

alde_alche,35,186,5	script	炼金公会里的会员	1_M_SIGNALCHE,{
	mes "[炼金公会里的会员]";
	if (BaseLevel > 98 && JobLevel > 49){
		if (BaseJob == Job_Alchemist) {
			if (SkillPoint != 0) {
				mes "- 请点完你的技能点. -";
				close;
			}
			if (job_gen == 0) {
				mes "我们炼金术师公会带来了许多各种炼金促销活动和调查.";
				next;
				mes "[炼金公会里的会员]";
				mes "我们的调查与活动已经蔓延到符文尘世的各个角落,例如各种特殊项目的生产方法.";
				next;
				mes "[炼金公会里的会员]";
				mes "我们称呼我们工会的新血为 ^3131FF'基因学者'^000000.";
				next;
				mes "[炼金公会里的会员]";
				mes "我们基因学家的精英目前在异世界里做调查.";
				next;
				mes "[炼金公会里的会员]";
				mes "你看起来像是在这方面的专家,你要去见见其他基因学家吗?";
				next;
				mes "[炼金公会里的会员]";
				mes "不久前有一位基因学家宣布了一项厉害的研究.";
				mes "你看起来很聪明,你要试一下吗?";
				next;
				mes "[炼金公会里的会员]";
				mes "她的研究可以帮助您获得更多的知识,以帮助你变成一位基因学家!";;
				next;
				mes "[炼金公会里的会员]";
				mes "怎样?你要去见见那位基因学家吗?";
				next;
				switch(select("- 当然,我要去见他.:- 不,我不要.")) {
				case 1:
					mes "[炼金公会里的会员]";
					mes "明智的选择!";
					mes "臭名昭著的基因学家'德瑞斯'.";
					next;
					mes "[炼金公会里的会员]";
					mes "你可以在里希塔乐镇的西边的炼金研究室找到他. 那里也有一位基因在武器研究室那,你也可以去找找他.";
					set job_gen,1;
					setquest 2215;
					close;
				case 2:
					mes "[炼金公会里的会员]";
					mes "难道你不对生命的研究感到兴趣?";
					close;
				}
			}
			mes "你可以在里希塔乐镇的西边的炼金研究室找到他.";
			mes "如果你有兴趣,你可以去找他.";
			close;
		}
	}
	mes "我们炼金术师公会带来了许多各种炼金促销活动和调查.";
	next;
	mes "[炼金公会里的会员]";
	mes "我们的调查与活动已经蔓延到符文尘世的各个角落,例如各种特殊项目的生产方法";
	next;
	mes "[炼金公会里的会员]";
	mes "我们称呼我们工会的新血为 ^3131FF'基因学者'^000000.";
	next;
	mes "[炼金公会里的会员]";
	mes "我们基因学家的精英目前在异世界里做调查.";
	close;
}

job3_gen01,25,58,3	script	德瑞斯#gen	4_LGTSCIENCE,{
	if (SkillPoint != 0) {
		mes "- 你还有技能点没点完. -";
		close;
	}
	if (job_gen == 1) {
		mes "[德瑞斯]";
		mes "哦!样品C的吸血植物的再次萎缩.";
		mes "我应该改变增长的护理的成分?";
		mes "请快振作起来!";
		next;
		mes "[德瑞斯]";
		mes "哦?有一位陌生人?";
		mes "你的样子告诉了我.你是一位炼金术师?";
		next;
		mes "[德瑞斯]";
		mes "现在炼金术士越来越多,但是没有多少人可以正确地研究这个领域!";
		next;
		mes "[德瑞斯]";
		mes "他们都不敢说这样的话,'看看我炸弹的伤害!''或''我的植物是不是更加的美丽''";
		next;
		mes "[德瑞斯]";
		mes "炼金术士公会说他们会支持那些研究,但实际上他们只能研究一些蘑菇'!";
		next;
		mes "[德瑞斯]";
		mes "感谢,那些研究蘑菇的人!现在我的实验室都是蘑菇!!";
		next;
		select("为什么你说这种话");
		mes "[德瑞斯]";
		mes "'因为我是来自异世界!";
		mes "你有去过异世界吗? 那些在异世界的人都这样说.";
		next;
		switch(select("- 不可能?!:- 我也是来自异世界.")) {
		case 1:
			mes "[德瑞斯]";
			mes "不可能?";
			mes "你是来自异世界的小村庄?";
			mes "我那里的人都这样说.";
			break;
		case 2:
			emotion e_lv;
			mes "[德瑞斯]";
			mes "真的?难过我觉得你有点眼熟u.";
			mes "在这里可以遇见同乡还真是开心!";
			break;
		}
		next;
		mes "- 扑通!! -";
		next;
		emotion e_ag;
		mes "[德瑞斯]";
		mes "啊!那个女人又再来!";
		next;
		select("- 什么声音?");
		mes "[德瑞斯]";
		mes "那是我的室友在做些研究, 德米 卡贝尔..";
		mes "他总是那么的吵.";
		next;
		emotion e_sob;
		mes "[德瑞斯]";
		mes "看!";
		mes "我的孢子和植物都吓坏了!";
		mes "我再也不能在这种环境里工作!! 太压力了!!";
		next;
		mes "- 用力咬 -";
		next;
		mes "[德瑞斯]";
		mes "当我觉得心情不好,我就会吃奥尔良先生的手工饼干.";
		next;
		mes "[德瑞斯]";
		mes "这是我从普隆德拉买的高品质的饼干.当我吃了一口,我就感觉到我在天上漂浮.";
		mes "这些饼干的可以让我放松心情.";
		next;
		mes "[德瑞斯]";
		mes "要试一下吗?";
		mes "啊,我吃完了.";
		next;
		mes "[德瑞斯]";
		mes "没关系!我二楼的房间还有很多箱呢!";
		mes "我不能停止购买哪些饼干.";
		next;
		mes "- Zooooooooooooooooing -";
		specialeffect EF_ENDURE;
		next;
		specialeffect EF_REPAIRWEAPON,AREA,"#from1to2gen";
		mes "- (刺耳的声音)!!! -";
		mes "- (刺耳的声音)!!! -";
		next;
		specialeffect EF_CRASHEARTH,AREA,"#from1to2gen";
		mes "- 扑通!扑通!撞! -";
		next;
		mes "[德瑞斯]";
		mes "..................";
		next;
		mes "[德瑞斯]";
		mes "耶~~";
		mes "耶~~!!!";
		next;
		mes "[德瑞斯]";
		mes "德米米米米米!!!";
		mes "德米米米米米!!!";
		next;
		mes "- 目前看来 -";
		mes "- 需要把德瑞斯冷静下来. -";
		set job_gen,2;
		close;
	} else if (job_gen == 2) {
		mes "[德瑞斯]";
		mes "耶~~!!!";
		mes "德米!!!";
		mes "德米!!!";
		next;
		mes "- 考虑中. -";
		mes "- 怎样才能让他冷静下来? -";
		close;
	} else if (job_gen == 3) {
		mes "[德瑞斯]";
		mes "耶!!!";
		mes "德米!!!";
		mes "德米!!!";
		next;
		select("给德瑞斯一个饼干.");
		mes "- 德瑞斯抢了 -";
		mes "- 哪些饼干 -";
		mes "- 然后就吃了. -";
		next;
		mes "- 咬咬咬 -";
		mes "- 咬咬咬 -";
		mes "- 咬咬咬 -";
		next;
		mes "[德瑞斯]";
		mes "啊,哪些奶油味还停留在我的嘴里.";
		mes "这可以让放松下来.";
		next;
		emotion e_pif;
		mes "[德瑞斯]";
		mes "德米就和平常一样在读书";
		mes "呼呼呼. 她真是一位勤劳的人.";
		next;
		mes "- 哪些饼干迅速了改变她的心情. -";
		emotion e_no1,1;
		next;
		mes "[德瑞斯]";
		mes "你喜欢炼金么?";
		next;
		mes "[德瑞斯]";
		mes "等等, 我们还没有自我介绍呢.";
		mes "我的名字是德瑞斯.";
		mes "我喜欢制造一些新的物品就好像一些新的植物.";
		next;
		mes "[德瑞斯]";
		mes "那叫什么名字,年轻的炼金术士?";
		next;
		select("- 我叫 "+strcharinfo(0)+".");
		mes "[德瑞斯]";
		mes "哦, "+strcharinfo(0)+"!";
		mes "好好听的名字. 很荣幸可以认识你";
		next;
		mes "[德瑞斯]";
		mes "但是我还不能记住你的名字.";
		mes "我可以称呼你为<噗噗>. 可以吗?";
		next;
		select("- 我...");
		mes "[德瑞斯]";
		mes "很好...";
		mes "噗噗, 你有听说过我的研究么?";
		mes "你来这里就是因为这件事吧?";
		next;
		switch(select("- 不.:- 是.")) {
		case 1:
			mes "[德瑞斯]";
			mes "都写在你脸上了.";
			mes "不需要害羞呀.";
			break;
		case 2:
			mes "[德瑞斯]";
			mes "是不是?";
			mes "我期望的是我可以做些东西可以让我们的同乡聚在一起";
			break;
		}
		next;
		mes "[德瑞斯]";
		mes "当你看到我那疯狂的状态,你可以做我之前所说的东西,而我平静下来.";
		next;
		mes "[德瑞斯]";
		mes "你喜欢你细心管理的方式.";
		mes "你很有才能成为一位基因学者.";
		next;
		mes "[德瑞斯]";
		mes "^FF0000如果你成为一名基因学者,你就不能研究炼金的东西了.^000000";
		mes "选择权是由你来决定.你现在要不要试一下?.";
		next;
		switch(select("- 等会才试.:- 我要成为一位基因学者!")) {
		case 1:
			mes "[德瑞斯]";
			mes "好的.";
			mes "虽然你没有基因学者般的知识,可是一些的基本知识也是很重要的.";
			mes "噗噗, 回来当你完成了你的学习.";
			set job_gen,4;
			close;
		case 2:
			mes "[德瑞斯]";
			mes "很好!";
			mes "然我们一起做!";
			set job_gen,5;
			close;
		}
	} else if (job_gen == 4) {
		mes "[德瑞斯]";
		mes "噗噗!";
		mes "你准备好了吗?";
		next;
		switch(select("- 还没.:- 好了!")) {
		case 1:
			mes "[德瑞斯]";
			mes "你要尽你的全力!";
			close;
		case 2:
			mes "[德瑞斯]";
			mes "很好!";
			mes "让我们一起来!";
			set job_gen,5;
			close;
		}
	} else if (job_gen == 5) {
		mes "[德瑞斯]";
		mes "你觉得这颗植物怎样, 噗噗";
		mes "你喜欢植物吗? 你喜欢怎样的植物";
		next;
		mes "- 爆,爆,爆 -";
		next;
		emotion e_swt;
		mes "[德瑞斯]";
		mes "别告诉我德米 卡贝尔又来了?";
		mes "别被我说中了德米...";
		next;
		mes "[德瑞斯]";
		mes "我说到哪了";
		mes "好的. 植物对我们很重要.";
		next;
		mes "[德瑞斯]";
		mes "基因学者也可以制作不同的植物来帮助我们战斗...就好像那些炼金术士";
		next;
		mes "- 爆炸!! -";
		next;
		emotion e_dots;
		mes "[德瑞斯]";
		mes ".......................";
		next;
		emotion e_dots;
		mes "[德瑞斯]";
		mes "植物的实验...";
		next;
		mes "- 爆炸!!!!!! -";
		next;
		emotion e_dots;
		mes "[德瑞斯]";
		mes "杂交...";
		next;
		mes "- 噗噗!!!!!! -";
		mes "- 啊!!!! -";
		mes "- 爆炸了!!!! -";
		mes "- 爆炸!! -";
		next;
		emotion e_dots;
		mes "[德瑞斯]";
		mes ".......................";
		next;
		emotion e_dots;
		mes "[德瑞斯]";
		mes "噗噗.";
		next;
		emotion e_dots;
		mes "[德瑞斯]";
		mes "我会给你一个基因学者的测验.";
		next;
		mes "[德瑞斯]";
		mes "题目是 <完美的隔音系统>!";
		mes "你可以找个方式不让德米 凯拉布莱尼那么的吵. 这里的实验室一楼有些植物及实验设备,你可以拿来用.";
		next;
		mes "[德瑞斯]";
		mes "你最好现在就开始!";
		set job_gen,6;
		close;
	} else if (job_gen == 6) {
		mes "[德瑞斯]";
		mes "题目是 <完美的隔音系统>!";
		mes "你可以找个方式不让德米 凯拉布莱尼那么的吵. 这里的实验室一楼有些植物及实验设备,你可以拿来用.";
		next;
		mes "[德瑞斯]";
		mes "那个柜子里有说明书.";
		mes "你最好现在就开始!";
		close;
	} else if (job_gen > 6 && job_gen < 60) {
		mes "有问题吗?";
		next;
		switch(select("- 没事.:- 实验失败了.")) {
		case 1:
			mes "[德瑞斯]";
			mes "你最好现在就开始!!! 不然我会疯掉";
			close;
		case 2:
			mes "[德瑞斯]";
			mes "你可以重新开始啊?";
			mes "为什么你问我这些问题?";
			set job_gen,7;
			if (checkquest(2209) == 1) erasequest 2209;
			if (checkquest(2210) == 1) erasequest 2210;
			if (checkquest(2211) == 1) erasequest 2211;
			if (checkquest(2212) == 1) erasequest 2212;
			if (checkquest(2213) == 1) erasequest 2213;
			if (checkquest(2214) == 1) erasequest 2214;
			close;
		}
	} else if (job_gen == 60 || job_gen == 61) {
		mes "- 叮 叮 叮 -";
		next;
		mes "- 叮 叮 叮  -";
		next;
		mes "[德瑞斯]";
		mes "德米 卡贝尔到底在干什么?";
		mes "真吵!";
		next;
		mes "[德瑞斯]";
		mes "你学习好了 噗噗?";
		mes "给我那些种子.";
		next;
		if (countitem(6273) == 0 && countitem(6272) == 0) {
			mes "[德瑞斯]";
			mes "什么事?";
			mes "你怎么什么都学不到?";
			mes "再来一次.";
			set job_gen,7;
		} else {
			mes "[德瑞斯]";
			mes "我会看你的种子做得怎样.";
			mes "成绩如何?";
			next;
			mes "[德瑞斯]";
			mes "当你把一颗种子放在在超级中耕机,并调节温度...";
			next;
			mes "[德瑞斯]";
			mes "可恶!";
			next;
			if (job_gen == 60) {
				specialeffect EF_FLASHER,AREA,"超级中耕机#gen";
				specialeffect EF_PHARMACY_OK,AREA,"超级中耕机#gen";
				mes "[德瑞斯]";
				mes "噗噗, 看那个!";
				mes "这的确是一个神秘的植物t!";
				next;
				mes "[德瑞斯]";
				mes "耕耘机里面现在是充满了刺的茎!";
				mes "那个茎在墙上不断的增长,然后会越来越纠结!";
				next;
				mes "[德瑞斯]";
				mes "这就是, 噗噗!";
				mes "已这种植物的特性,可用于许多东西!";
				next;
				mes "[德瑞斯]";
				mes "非常的好, 噗噗.";
				mes "我就知道你很聪明可是你的能力不止这样!";
				next;
				mes "[德瑞斯]";
				mes "我会告诉炼金公会结果的.";
				mes "我会给封推荐信说你有很好的能力可以成为基因学者.";
				next;
				mes "[德瑞斯]";
				mes "要知道公会的答复需要点时间,你能等会嘛?";
				mes "为什么你不去找德米 凯拉布莱尼?";
				next;
				mes "[德瑞斯]";
				mes "他是一位基因学者,专门研究微型大炮.";
				mes "去看看他的研究吧.";
				mes "你可以帮我转达给他一件事吗? 告诉她请静静地进行她的实验.";
				delitem 6273,1; //Altered_Seed
				set job_gen,62;
				changequest 2215,2216;
			} else {
				specialeffect EF_SPELLBREAKER,AREA,"Super Cultivator#gen";
				specialeffect EF_PHARMACY_FAIL,AREA,"Super Cultivator#gen";
				mes "[德瑞斯]";
				mes "你失败了, 噗噗.";
				mes "一点的成长都没有,不是说都写在那里了吗.";
				mes "去!重新开始";
				delitem 6272,1; //Experiment_Seed
				set job_gen,7;
			}
		}
		if (checkquest(2209) == 1) erasequest 2209;
		if (checkquest(2210) == 1) erasequest 2210;
		if (checkquest(2211) == 1) erasequest 2211;
		if (checkquest(2212) == 1) erasequest 2212;
		if (checkquest(2213) == 1) erasequest 2213;
		if (checkquest(2214) == 1) erasequest 2214;
		close;
	} else if (job_gen > 61 && job_gen < 76) {
		if (checkquest(2223,HUNTING) == 2) {
			mes "[德瑞斯]";
			mes "请稍等.";
			mes "看来信息到了.";
			set job_gen,76;
			completequest 2223;
			close;
		}
		mes "[德瑞斯]";
		mes "你可以去参观德米 卡贝尔的工作地当我们在等着公会的答复.";
		close;
	} else if (job_gen == 76) {
		mes "[德瑞斯]";
		mes "噗噗!";
		mes "我有个好消息告诉你!";
		next;
		if (checkweight(1201,2) == 0 || MaxWeight - Weight < 2000) {
			mes "- 等会 !! -";
			mes "- 你现在拿着 -";
			mes "- 太多的东西了. -";
			mes "- 请重试 -";
			mes "- 当你收拾好你的东西. -";
			close;
		}
		mes "[德瑞斯]";
		if (BaseJob != Job_Alchemist || BaseLevel < 99 || JobLevel < 50) {
			mes "看起来你还没准备好.";
			close;
		}
		if (checkcart()){
			mes "请卸下你的推车.";
			close;
		}
		if (SkillPoint != 0) {
			mes "请点完你的技能.";
			close;
		}
		mes "收到公会的答复了.";
		mes "我帮你读出来, 噗噗!";
		next;
		mes "<亲爱的德瑞斯小姐,>";
		mes "<我们很欣赏你的研究.>";
		next;
		mes "<德瑞斯小姐>";
		mes "这位兄弟也是我们的同乡.";
		next;
		mes "<一个非常好的消息,你的研究将作为商业用途.>";
		mes "<我敢肯定,这将会对其他的炼金术士有很大的帮助.>";
		next;
		mes "<你已经被录取了>";
		mes "<有一位炼金术师发现了一个能让植物快速成长的东西, "+strcharinfo(0)+">";
		next;
		mes "<我和其他的炼金术士都这项研究很感到兴趣.>";
		next;
		mes "<我们期待着会有更多的研究成果.>";
		next;
		mes "<德瑞斯小姐>";
		mes "<我觉得 "+strcharinfo(0)+" 已经有资格成为基因学者了.>";
		next;
		mes "<请尽快告诉 "+strcharinfo(0)+" 其他的基因学者这项研究.>";
		next;
		mes "<我们炼金术士对德瑞斯小姐和德米 卡贝尔小姐 和 "+strcharinfo(0)+" 感到非常的光荣.>";
		mes "我们希望你们可以做出对水果和蔬菜的研究.>";
		next;
		mes "[德瑞斯]";
		if (ismounting()) {
			mes "你骑着坐骑,";
			mes "所以你不能转职.";
			mes "请卸下你的坐骑!";
			close;
		}
		mes "恭喜, 噗噗!";
		mes "你现在是基因学者了!!";
		jobchange roclass(eaclass()|EAJL_THIRD);
		set job_gen,77;
		getitem 5752,1; //Midas_Whisper
		getitem 2795,1; //Green_Apple_Ring
		next;
		mes "[德瑞斯]";
		mes "就和炼金术士公会所说的,我们期待着你将来的研究.";
		mes "一起加油!";
		close;
	} else if (job_gen == 77) {
		mes "[德瑞斯]";
		mes "噗噗,你想加入我爱新奥尔良的俱乐部?这是一个爱饼干俱乐部,当然我们并不认为做这样奇怪的事情是有问题的.";
		close;
	}
	mes "[德瑞斯]";
	mes "你对我用兴趣吗? 为什么一直盯着我看";
	close;
}

job3_gen01,83,72,3	script	箱子#generic	HIDDEN_NPC,{
	mes "- 好大的箱子. -";
	if (job_gen == 2) {
		next;
		switch(select("- 开箱子.:- 检查地址.:- 不做任何事.")) {
		case 1:
			mes "- 箱子里有很多饼干. -";
			next;
			switch(select("- 拿饼干出来.:- 不做任何事.")) {
			case 1:
				mes "- 你拿了饼干出来. -";
				set job_gen,3;
				close;
			case 2:
				mes "你没做任何事.";
				close;
			}
		case 2:
			mes "[来自: 卢恩.米德加兹王国, 普隆德拉, 查理斯 奥兰斯]";
			mes "[接受: 秀发兹发德 教国, 里希塔乐镇, 德瑞斯]";
			close;
		case 3:
			mes "你没做任何事.";
			close;
		}
	}
	close;
}

job3_gen01,12,56,3	script	植物指南#generic	HIDDEN_NPC,{
	mes "<世界上最简单的炼金术>";
	mes "作者博瑞斯.";
	next;
	switch(select("- 动物:- 植物:- 材料:- 其他")) {
	case 1:
		mes "...任何一个炼金术师都可以炼金动物";
		mes "你可以使用白色托盘,来炼金动物.";
		mes "I这是非常简单的.";
		next;
		mes "...可是炼金动物是非常危险的.";
		mes "所以我不会使用它们.";
		close;
	case 2:
		mes "...可是炼金植物是非常的简单.";
		mes "你可以把植物放在桌子上,并破坏或切断他们.";
		mes "这是一个非常厉害的方法.";
		next;
		mes "...那些炼金植物通常是 Bigibigi草和母卡树它们有尖锐的刺然后长得非常的快.";
		if (job_gen == 6)
			set job_gen,7;
		close;
	case 3:
		mes "...用一些材料来炼金出东西是非常简单的.";
		mes "你可以把一些材料放在在精美的盒子,然后炼金出来.";
		mes "这也是一种方法.";
		next;
		mes "...那个材料是'强化武器金属-级数一'而且很便宜.";
		close;
	case 4:
		mes "...炼金术是一些非常简单的东西.";
		mes "我们每天会有一些炼金术师来做一些实验给初学者看.";
		mes "我肯定当你看完这本书,你会喜欢上炼金术.";
		close;
	}
}

// QuestID,PlantName,Description{,Part?}
function	script	Genetic_Job_Plant	{
	if (job_gen == 7) {
		if (checkquest(getarg(0)) == -1 || checkquest(getarg(0)) == 0) {
			mes "- "+getarg(2)+" -";
			next;
			switch(select("- 用这个材料.:- 不使用.")) {
			case 1:
				if (getarg(3,0)) set .@str$,"一些 ";
				mes "- 你已经获得 "+.@str$+getarg(1)+". -";
				setquest getarg(0);
				close;
			case 2:
				mes "- 这个植物看起来不能被使用. -";
				close;
			}
		}
		mes "- 你已经获得"+getarg(1)+". -";
		close;
	} else if (job_gen == 6) {
		mes "这颗植物可以在植物类的柜子里找到.";
		close;
	}
	mes "- "+getarg(2)+" -";
	close;
}

job3_gen01,36,55,3	script	Bigibigi Grass#gen	CLEAR_NPC,{
	callfunc "Genetic_Job_Plant",2209,"Bigibigi Grass","这个尺寸比起普通的植物还大我们叫它为Bigibigi Grass.";
	end;
}
job3_gen01,36,45,3	script	Muka Tree#gen	CLEAR_NPC,{
	callfunc "Genetic_Job_Plant",2210,"Muka Tree","这颗姆卡树可以保护内部的尖锐的刺.",1;
	end;
}
job3_gen01,42,55,3	script	Bogi Vine#gen	CLEAR_NPC,{
	callfunc "Genetic_Job_Plant",2211,"Bogi Vine","这是一个Bogi藤,它的增长速度非常快.",1;
	end;
}
job3_gen01,30,58,3	script	Aolatura#gen	CLEAR_NPC,{
	callfunc "Genetic_Job_Plant",2212,"Aolatura","这颗千年花一生只开一次花.";
	end;
}
job3_gen01,30,63,3	script	Congra#gen	CLEAR_NPC,{
	callfunc "Genetic_Job_Plant",2213,"Congra","这是一颗巨大的植物有有着坚实的茎.";
	end;
}
job3_gen01,32,38,3	script	Sticky Grass#gen	CLEAR_NPC,{
	callfunc "Genetic_Job_Plant",2214,"Sticky Grass","我可能不知道这个名字,但它的粘性,可以粘到任何的东西.";
	end;
}

job3_gen01,7,50,3	script	测试表格#gen	CLEAR_NPC,{
	if (job_gen == 7) {
		mes "- 这里有很多设备. 我想我可以在这里读关于的植物的组合. -";
		next;
		if(select("- 进行实验.:- 不进行实验.") == 2) {
			mes "- 这不是启动一个实验的时间. -";
			close;
		}
		if ((checkquest(2209) == -1 || checkquest(2209) == 0) &&
		    (checkquest(2210) == -1 || checkquest(2210) == 0) &&
		    (checkquest(2211) == -1 || checkquest(2211) == 0) &&
		    (checkquest(2212) == -1 || checkquest(2212) == 0) &&
		    (checkquest(2213) == -1 || checkquest(2213) == 0) &&
		    (checkquest(2214) == -1 || checkquest(2214) == 0)) {
			mes "["+strcharinfo(0)+"]";
			mes "唔, 我需要先收集植物材料.";
			close;
		}
		mes "我该怎么用这些植物材料?";
		next;
		select("- 打烂.:- 打成碎片.:- 压碎.");
		while (1) {
			mes "现在我该怎么做";
			next;
			if (rand(2))
				select("- 燃烧成灰烬.:- 浸入水里.:- 蒸.");
			else
				select("- 把它放入一个测试瓶然后再摇.:- 冷冻.:- 加热.");
			if (!rand(7)) break;
		}
		mes "现在我该怎么做?";
		next;
		select("把它注入一个测试种子.");
		mes "W我应该选择什么样的种子?";
		next;
		select("测试种子 - 种子A - 种子B - 种子C - 种子D");
		mes "我已经把准备好的材料注射到测试种子.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "好.现在,我可以让德瑞斯一起来看看这个种子了!";
		if ((checkquest(2209) == -1 || checkquest(2209) == 0) &&
		    (checkquest(2210) == -1 || checkquest(2210) == 0) &&
		    (checkquest(2211) == -1 || checkquest(2211) == 0)){
			set job_gen,61;
			getitem 6272,1; //Experiment_Seed
			close;
		}
		if (checkquest(2209) == 1) set job_gen,job_gen+10;
		if (checkquest(2210) == 1) set job_gen,job_gen+10;
		if (checkquest(2211) == 1) set job_gen,job_gen+10;
		set .@score, job_gen + JobLevel - 20;
		if (rand(1,100) < .@score) {
			set job_gen,60;
			getitem 6273,1; //Altered_Seed
			close;
		} else {
			set job_gen,61;
			getitem 6272,1; //Experiment_Seed
			close;
		}
	} else if (job_gen > 7 && job_gen < 60) {
		mes "- 实验失败. -";
		set job_gen,7;
		if (countitem(6273)) delitem 6273,1; //Altered_Seed
		if (countitem(6272)) delitem 6272,1; //Experiment_Seed
		if (checkquest(2209) == 1) erasequest 2209;
		if (checkquest(2210) == 1) erasequest 2210;
		if (checkquest(2211) == 1) erasequest 2211;
		if (checkquest(2212) == 1) erasequest 2212;
		if (checkquest(2213) == 1) erasequest 2213;
		if (checkquest(2214) == 1) erasequest 2214;
		close;
	} else if (job_gen == 60 || job_gen == 61) {
		mes " - 好.现在,我可以让德瑞斯一起来看看这个种子了! -";
		end;
	}
	mes "- 这里有很多设备. 我想我可以在这里读关于的植物的组合. -";
	close;
}

job3_gen01,21,67,3	script	超级中耕机#gen	CLEAR_NPC,{
	end;
}

job3_gen01,91,48,3	script	德米 卡贝尔#gen	4_F_SCIENCE,{
	mes "[德米 卡贝尔]";
	if (job_gen < 62) {
		mes "螺丝去哪了?";
		close;
	} else if (job_gen == 62) {
		mes "那蓝色的手推车~";
		mes "加载的图片看起来像是大炮~";
		mes "与我可爱的宝贝~";
		mes "想永远研究大炮~~";
		next;
		mes "[德米 卡贝尔]";
		mes "螺丝去哪了?";
		mes "为什么螺丝会不够? 我不是刚刚才定了吗?";
		next;
		select("- 你好~?");
		mes "[德米 卡贝尔]";
		mes "我的天呀~?";
		mes "你是谁?";
		next;
		mes "[德米 卡贝尔]";
		mes "我的天呀!";
		mes "来帮帮我!";
		next;
		mes "[德米 卡贝尔]";
		mes "我需要今天完成这个迷你机炮,但我找不到其他零件!";
		next;
		mes "[德米 卡贝尔]";
		mes "它们应该被放在某个地方了.";
		mes "我只需要10个? 请帮帮我!";
		changequest 2216,2217;
		set job_gen,63;
		close;
	} else if (job_gen > 62 && job_gen < 74) {
		mes "哦不~~~";
		mes "你找他其他零件了吗!";
		close;
	} else if (job_gen == 74) {
		mes "这个车火力就做不好,我们来把它重组...";
		next;
		select("- 我已经得到了部分.");
		mes "[德米 卡贝尔]";
		mes "我的天!";
		mes "你吓死我了!!";
		mes "这些是弄丢的螺丝吗?";
		mes "你在哪找到的?";
		next;
		select("你吩咐我找的啊!");
		mes "[德米 卡贝尔]";
		mes "哦? 是吗?";
		mes "我有吗? 嘻嘻.";
		mes "我自己也不知道.";
		next;
		mes "[德米 卡贝尔]";
		mes "请稍等.";
		mes "很快就好了.";
		next;
		mes "- 转转转 -";
		next;
		mes "[德米 卡贝尔]";
		mes "我完成这个手推车了. 我们来试一下威力巴?";
		next;
		mes "- 斯... -";
		next;
		mes "- 砰砰砰!! -";
		next;
		mes "[德米 卡贝尔]";
		mes "很好! 成功了!";
		mes "哇, 你帮了我一个大忙.";
		mes "谢谢你.";
		next;
		mes "[德米 卡贝尔]";
		mes "你为什么来这里?";
		mes "你是不是要来订点东西";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我是一个基因学者的工作人员人.";
		mes "我听说你很有才能.";
		next;
		mes "[德米 卡贝尔]";
		mes "是的, 我处理过很多种火炮.";
		mes "那些想要改造自己的车都是我在装的,我可以添加一些功能,例如迷你大炮安装在他们的推车.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "改装手推车?";
		next;
		mes "[德米 卡贝尔]";
		mes "是的,你没想过参加一些赛跑比赛吗?";
		mes "还是你没想过改装这些无聊的老推车吗?";
		next;
		mes "[德米 卡贝尔]";
		mes "手推车不再只是用来放东西.";
		mes "你可以改装成可以帮你战斗的手推车.";
		next;
		mes "[德米 卡贝尔]";
		mes "德瑞斯小姐看起来还在学习一些无聊的东西.";
		mes "我不明白为什们那些东西会那么的受欢迎.";
		emotion e_pif;
		next;
		mes "[德米 卡贝尔]";
		mes "看这些迷你炮.";
		mes "这是世界上最小和最轻的炮";
		mes "很可爱对吧~";
		emotion e_awsm;
		next;
		select("- 我可以改装我的手推车吗?");
		mes "[德米 卡贝尔]";
		mes "不幸的是,只有基因学者被允许改造车.";
		mes "这是炼金公会的规矩.";
		next;
		mes "[德米 卡贝尔]";
		mes "可是别失望.";
		mes "你也可以成为一位基因学者!";
		mes "哈哈哈.";
		next;
		mes "[德米 卡贝尔]";
		mes "我可以改装任何一个手推车.";
		mes "可是需要一个质量很好的手推车.";
		next;
		mes "[德米 卡贝尔]";
		mes "你应该练习你的手推车技能直到你变成了基因学者.";
		mes "你应该锻炼你的肌肉, 不然你不能驾驭这些被改装的手推车,";
		next;
		mes "[德米 卡贝尔]";
		mes "如果你需要, ^FF0000我可以介绍一个地方让你练习.^000000";
		mes "^FF0000德瑞斯不太喜欢这里^000000, 可是这是一个很棒的地方. 哈哈.";
		next;
		mes "[德米 卡贝尔]";
		mes "现在你可以摆动你的手推车来练习练习";
		mes "当你可以摆动至少 ^FF0000100^000000 次, 你就可以驾驭改装的手推车.";
		next;
		mes "[德米 卡贝尔]";
		mes "我需要工作了.";
		mes "来找我当你需要练习手推车.";
		mes "当你需要改装我会帮你的.";
		next;
		mes "- 嘎嘎声 -";
		next;
		mes "- 这是什么?我觉得地板在晃动. -";
		set job_gen,75;
		changequest 2217,2223;
		close2;
		warp "job3_gen01",23,32;
		end;
	} else if (job_gen == 75) {
		if (checkquest(2223,HUNTING) == 2) {
			mes "看起来德瑞斯小姐已经知道到我把你放在他的蘑菇笔里了.";
			mes "当她知道你把他的蘑菇都杀光了, 他的反应一定很好笑.";
			close;
		}
		mes "你还没练习好吗?";
		mes "你要进入蘑菇笔吗?";
		next;
		switch(select("- 是.:- 不.")) {
		case 1:
			mes "[德米 卡贝尔]";
			mes "有趣吧~";
			close2;
			warp "job3_gen01",23,32;
			end;
		case 2:
			mes "[德米 卡贝尔]";
			mes "我要看看德瑞斯生气的样子.";
			close;
		}
	} else if (job_gen > 75) {
		mes "看起来德瑞斯已经知道到我把你放在他的蘑菇笔里了.";
		mes "当她知道你把他的蘑菇都杀光了, 他的反应一定很好笑.";
		close;
	}
}

-	script	Scattered Stuff#gen	-1,{
	if (job_gen > 62 && job_gen < 74) {
		mes "- 这个实验室有好多散零件. -";
		next;
		mes "- 你发现了一个小金属碎片. -";
		set job_gen, job_gen+1;
		specialeffect EF_BASH;
		disablenpc strnpcinfo(0);
		initnpctimer;
		close;
	} else if (job_gen == 74) {
		mes "- 我应该收集好了. -";
		close;
	}
	end;
OnTimer7000:
	enablenpc strnpcinfo(0);
	stopnpctimer;
	end;
}
job3_gen01,84,45,3	duplicate(Scattered Stuff#gen)	散落的东西#gen1	HIDDEN_NPC
job3_gen01,77,41,3	duplicate(Scattered Stuff#gen)	散落的东西#gen2	HIDDEN_NPC
job3_gen01,78,57,3	duplicate(Scattered Stuff#gen)	散落的东西#gen3	HIDDEN_NPC
job3_gen01,89,59,3	duplicate(Scattered Stuff#gen)	散落的东西#gen4	HIDDEN_NPC
job3_gen01,90,51,3	duplicate(Scattered Stuff#gen)	散落的东西#gen5	HIDDEN_NPC
job3_gen01,83,27,3	duplicate(Scattered Stuff#gen)	散落的东西#gen6	HIDDEN_NPC
job3_gen01,74,45,3	duplicate(Scattered Stuff#gen)	散落的东西#gen7	HIDDEN_NPC

job3_gen01,18,39,3	script	警告#gen	2_BULLETIN_BOARD,{
	mes "= 警告 =";
	mes "如果你欺负我的蘑菇!!! 我会杀死你";
	if (job_gen < 75) close;
	next;
	switch(select("- 现在出来.:- 留下.")) {
	case 1:
		mes "- 我最好现出来别让德瑞斯小姐抓到我在这里. -";
		close2;
		warp "job3_gen01",21,38;
		end;
	case 2:
		mes "- 我决定留下. -";
		close;
	}
}

job3_gen01,80,12,1	script	一叠杂志#gen	HIDDEN_NPC,{
	mes "- 'Monthly Bang!' 杂志里都是乱码. -";
	close;
}

job3_gen01,83,20,1	script	床#gen	HIDDEN_NPC,{
	mes "- 这个精心挑选的图案的毯子放在这个肮脏的床. 看起来好浪费 -";
	close;
}

job3_gen01,85,25,1	script	翻开的书#gen	HIDDEN_NPC,{
	mes "< 你想要我的漂亮的房间?你有足够的金币?是否有足够的空间? 现在马上到里希塔乐镇的百货就可以买到最高端的墙纸及室内配件! >";
	next;
	mes "< 美丽的蜡烛会让你的房间看起来更美. 每一个女人都想拥有一个巨大的美丽的金色镜子放在房间. 你不能错过任何的东西! >";
	close;
}

job3_gen01,81,81,0	script	#dbroom	HIDDEN_WARP_NPC,2,2,{
OnTouch:
	mes "- 这间房间开起来好像仓库. 好多包裹在这里. -";
	close;
}

job3_gen01,24,32,0	script	德瑞斯的注视	HIDDEN_WARP_NPC,2,2,{
OnTouch:
	if (checkquest(2223,HUNTING) == 2) {
		mapannounce "job3_gen01","德瑞斯: 哇呃? 在那里丢失的猫咪在孢子的试管里?",bc_map,"0x99CC00"; //FW_NORMAL 12 0 0
		emotion e_omg,1;
		warp "job3_gen01",21,38;
	}
	end;
}

job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0