//===== Hercules Script ======================================
//= Royal Guard Job Quest
//===== By: ==================================================
//= Muad_Dib
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= [Official Conversion]
//= Job change Quest from Crusader / Paladin -> Royal Guard.
//===== Additional Comments: =================================
//= 1.0 First Version. [brAthena]
//= 1.1 Updated script. [Euphy]
//= 1.2 Added GM management function. [Euphy]
//============================================================

prt_castle,48,161,3	script	中年绅士#rg	1_M_LIBRARYMASTER,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- 你的包裹太重了. -";
		mes "- 先减轻你的身体再说吧 -";
		mes "- 然后再试试. -";
		close;
	}
	if (Class == Job_Royal_Guard || Class == Job_Royal_Guard_T || Class == Job_Baby_Guard) {
		mes "[中年绅士]";
		mes "嗯? 我在干什么啊? 我来这里是有使命要完成的...";
		next;
		mes "^000099也许我的感觉已经传给另外的人了.^000000";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "[中年绅士]";
		mes "哟你还真年经啊. 继续努力保持工作.";
		close;
	}
	if (checkriding() || checkcart() || checkfalcon()) {
		mes "[中年绅士]";
		mes "你好像在拖拉着什么东西. 如果你骑在什么东西上最好先下来再开始吧.";
		close;
	}
	if (BaseJob != Job_Crusader) {
		mes "[中年绅士]";
		mes "我没有任何事情要告诉你. 去找其他人吧.";
		close;
	}
	if (job_royal == 0) {
		mes "[中年绅士]";
		mes "我在说什么啊... (喃喃自语) 其余的部分在克雷斯特汉姆古城的周围.......";
		next;
		mes "[中年绅士]";
		mes "嗯?";
		mes "你需要什么吗?";
		next;
		select("我以前没见过你.");
		mes "[中年绅士]";
		mes "呵呵呵呵, 别担心我.";
		next;
		mes "[中年绅士]";
		mes "如果你和我没有任何事情可谈, 但请保持好朋友的关系.";
		next;
		select("你看上去不像一个骑士啊...");
		mes "[中年绅士]";
		mes "你这个人真好奇. 你是谁呢? 你又想干嘛呢?";
		next;
		select("周围在看看!:这是我的办公室!:我是谁? 你又是谁?");
		mes "[中年绅士]";
		mes "你是想和我争论吗? 你难道不知道我是谁吗?";
		next;
		if(select("不太清楚, 你是谁?:对不起, 我不想伤害你.") == 2) {
			mes "["+strcharinfo(0)+"]";
			mes "对不起. 我要确认一下. 我不知道你是来自于皇家宫殿的.";
			next;
			mes "[中年绅士]";
			mes "够了. 不要在犯相同的错误了.";
			close;
		}
		mes "["+strcharinfo(0)+"]";
		mes "我是这里的成员我有权利知道你在这里想做什么.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "你好像第一次过来我不确定你是不是属于这里的.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "如果你告诉我你的身份, 我就先道个歉.";
		next;
		mes "[中年绅士]";
		mes "你真固执啊.";
		mes "如果你真的想这样, 我先做一下做我介绍吧.";
		next;
		mes "[中年绅士]";
		mes "我名字叫海因里希.";
		mes "我是因为这里的国王斯科米德忒兹失踪了, 在收集这方面的线索.";
		next;
		select("Schmidzt的线索? 这个是不是这里的宝物啊?");
		mes "[海因里希]";
		mes "所有的考古学家都渴望得到这个宝物. 我在努力找着方面的线索 ^000099皇家卫士^000000 和他们的儿子们都和这个斯科米德忒兹有关.";
		next;
		select("皇家卫士?");
		mes "[海因里希]";
		mes "是不是听起来很有趣啊? 我不能完全的说给你听关于他们的来历和档案, 这些都相当保密的.";
		next;
		select("你不能告诉我?");
		mes "[海因里希]";
		mes "我无法回答任何一个人询问我关于皇家卫士的事情.";
		next;
		mes "[海因里希]";
		mes "但是!! 我还是想相信你...";
		next;
		switch(select("- 我很想知道更多.:- 不继续聊了.")) {
		case 1:
			mes "[海因里希]";
			mes "你有点鲁莽.";
			next;
			mes "[海因里希]";
			mes "对你来说这不是一个简单的故事. 回去吧.";
			set job_royal,1;
			setquest 12090;
			close;
		case 2:
			mes "[海因里希]";
			mes "好主意.";
			next;
			mes "[海因里希]";
			mes "还是忘了这些吧, 去做些有意义的事情.";
			close;
		}
	}
	mes "[海因里希]";
	if (job_royal == 1) {
		mes "[海因里希]";
		mes "你这个朋友真固执啊.";
		mes "你有其他事情和我说吗?";
		next;
		select("如果你无法信任我, 那就考验我吧.");
		mes "[海因里希]";
		mes "考验?";
		mes "你是跟我说要考验吗? 呼!";
		next;
		mes "[海因里希]";
		mes "朋友啊, 你似乎对我的工作很有兴趣, 你真的确定吗? 或者你只是和我开玩笑?";
		next;
		select("我是认真的!");
		mes "[海因里希]";
		mes "真的吗?";
		mes "非常好. 你可能对皇家卫士有些错误的想法. 所以, 在你看来, 你认为成为一个皇家卫士的关键是什么?";
		next;
		select("- 决心和勇气:- 冷静的判断:- 敏锐的智慧:- 仁慈的心灵");
		mes "[海因里希]";
		mes "选择的好.";
		mes "事实上, 其余三个对于一个皇家卫士的精神来说也都重要, 我的朋友.";
		next;
		mes "[海因里希]";
		mes "但是根据调查记载, 皇家卫士最高的信仰是 '苦行和忍耐'.";
		next;
		mes "[海因里希]";
		mes "你明白了吗?";
		mes "年轻人, 苦行和忍耐. 和你回答的是有些不同的.";
		next;
		mes "[海因里希]";
		mes "你看起来对这个故事有着非常大的兴趣, 但是我不能再告诉你更多了.";
		next;
		mes "[海因里希]";
		mes "只能有资格的人才能知道皇家卫士的历史.";
		next;
		select("我怎么样做才能听这个故事?");
		mes "^000099海因里希很痛苦的看着你的脸. 他看起来就像睡着了一样. 最后他鲁莽的说到.^000000";
		next;
		mes "[海因里希]";
		mes "你实在太顽固了.";
		mes "如果这样我就来考验你苦行能力和忍耐力.";
		next;
		mes "[海因里希]";
		mes "很久以前国王斯科米德忒兹给一个拿回来100个神圣标记的勇敢战士一个头衔.";
		next;
		mes "[海因里希]";
		mes "如果你想听更多关于这个故事的内容, 你先证明你有相同的态度.";
		next;
		if(select("这是个不公平的要求.:我试试看.") == 1) {
			mes "[海因里希]";
			mes "好主意, 最好再发现一些有价值的东西.";
			close;
		}
		mes "[海因里希]";
		mes "好的, 一起就用这个方法做吧. 事实上收集100个神圣标记是非常难的, 就收集100个燃烧的心脏作为展示你的雄心吧.";
		next;
		mes "[海因里希]";
		mes "如果你带这些过来我就告诉你更多关于故事的内容. 这样做意味着你有很好的耐心.";
		next;
		mes "[海因里希]";
		mes "不要忘记带100个燃烧的心脏哦.";
		set job_royal,2;
		changequest 12090,12091;
		close;
	} else if (job_royal == 2) {
		if (countitem(7097) < 100) {
			mes "不要忘记带100个燃烧的心脏哦.";
			close;
		}
		mes "你都得到了吗?";
		mes "很多人忽略我并放弃了任务.";
		next;
		mes "[海因里希]";
		mes "等一下我把你带来的材料去处理一下.";
		delitem 7097,100; //Burning_Heart
		set job_royal,3;
		changequest 12091,12092;
		close;
	} else if (job_royal == 3) {
		mes "...";
		mes "非常好. 我感觉到你有一颗真挚的心. 现在我可以告诉你故事的更多内容.";
		next;
		select("好的, 告诉我更多吧.");
		mes "[海因里希]";
		mes "只有最好的十字军和帕拉丁才能获得做一名皇家卫士的资格.";
		next;
		mes "[海因里希]";
		mes "他们有着神圣的秩序和审判一切的惩罚.";
		next;
		mes "[海因里希]";
		mes "我们仍然有很多不同意见关于他们是如何幸存的. 当然有人还是相信他们的历史并不是虚构的.";
		next;
		select("这些都被记载了吗?");
		mes "[海因里希]";
		mes "嗯, 你说到了记载...";
		next;
		mes "[海因里希]";
		mes "很不幸的是当国王斯科米德忒兹 失踪后, 皇家卫士的踪迹也消失了.";
		next;
		mes "[海因里希]";
		mes "他们的历史, 信仰, 视野和战斗技巧看起来都要消声觅迹了, 没有任何文档记载了这些内容.";
		next;
		mes "[海因里希]";
		mes "就和国王一样一起失踪了.";
		next;
		select("那找寻这些踪迹的理由是...");
		mes "[海因里希]";
		mes "我很担心曾经的皇家卫士并没能把他们的历史给传承下来.";
		next;
		mes "[海因里希]";
		mes "这个神秘的力量... 不能就这么在世界上消失了. 这多么让人失望啊~!";
		next;
		mes "[海因里希]";
		mes "这就是为什么我要集中精力复兴他们的事业.";
		next;
		select("你进展到什么地步了?");
		mes "[海因里希]";
		mes "很幸运, 我发掘到很多宝藏可以解读到一些关于他们的事. 我只缺一个线索没有发现.";
		next;
		select("剩下什么线索?");
		mes "[海因里希]";
		mes "皇家卫士雕刻了一个国王斯科米德忒兹的标记在他们的衣服上. 他们的这个习惯也就是最后的一个线索.";
		next;
		mes "[海因里希]";
		mes "根据其他城市的报告可能在克雷斯特汉姆古城里的一个修道院里.";
		next;
		mes "[海因里希]";
		mes "但这个对我来说太危险了所以我几乎放弃了.";
		next;
		mes "[海因里希]";
		mes "现在我还不能告诉你为什么线索在那里.";
		next;
		if(select("- 有兴趣. 我能去那里吗?:- 看起来真的很危险.") == 2) {
			mes "[海因里希]";
			mes "好主意, 在那里搜索很费时间.";
			close;
		}
		mes "[海因里希]";
		mes "你开玩笑吗?";
		mes "你不能把他看作像做家务一样的简单.";
		next;
		select("我没有开玩笑. 我认真的.");
		mes "[海因里希]";
		mes "如果你这样说我也没有任何的选择.";
		next;
		mes "[海因里希]";
		mes "我不知道这会不会帮助到你. 我会写一个详细的说明.";
		next;
		mes "^000099这里是皇家卫士最后的踪迹. 打开任务窗口可以查到位置.^000000";
		set job_royal,4;
		changequest 12092,12093;
		close;
	} else if (job_royal == 4) {
		if (countitem(6274) == 0) {
			mes "我们的职员都已经准备好这个线索, 所以不用太过于努力.";
			close;
		}
		mes "不, 这个不是我想要的那个.";
		next;
		mes "[海因里希]";
		mes "你让我太惊讶了. 你怎么样拿到的啊? 那么久从来没人可以拿到这个.";
		next;
		mes "[海因里希]";
		mes "非常感谢.";
		mes "谢谢你的成果, 我能发现这最后的线索. 太惊讶了~!";
		next;
		mes "^000099海因里希 有点犹豫的从办公室的边上拿来一个破旧的盾牌.^000000";
		next;
		mes "[海因里希]";
		mes "我们职员发掘到的. 他们以为国王斯科米德忒兹用过这个盾. 但他好像没有用过.";
		next;
		select("确定这个盾国王没用过吗?");
		mes "[海因里希]";
		mes "我不能完全肯定.";
		mes "也许你是对的, 但是根据我们第一次的仔细鉴定我们能确定这个盾没有使用过.";
		next;
		mes "[海因里希]";
		mes "但是这个是一个多年前的宝物, 所以还是有利用价值的. 我也许知道怎么去用.";
		next;
		select("这个能给我吗?");
		mes "[海因里希]";
		mes "当然.";
		mes "也许有一天你会用上这个.";
		next;
		mes "^000099你从海因里希这里拿到了盾. 在盾的里面雕刻着一些字母.^000000 ";
		next;
		mes "^000099如果你想看这些字母, 请查看你的任务窗口.^000000";
		next;
		mes "[海因里希]";
		mes "好的~~ 现在.";
		mes "我准备去破译这些衣服上的标记所以我会非常的忙!";
		delitem 6274,1; //Saint_Cloth_Piece
		getitem 6275,1; //King_Shield
		set job_royal,5;
		changequest 12093,12094;
		close;
	} else if (job_royal == 5) {
		if (countitem(6275) == 0) {
			mes "什么? 你把盾丢了? 你背后是什么?";
			getitem 6275,1; //King_Shield
			close;
		}
		mes "谢谢你的帮助我能完成挖掘工作. 非常感谢.";
		close;
	}
}

glast_01,240,366,5	script	国王的记忆	CLEAR_NPC,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- 你的包裹太重了. -";
		mes "- 先减轻你的身体再说吧 -";
		mes "- 然后再试试. -";
		close;
	}
	if (ismounting() || checkriding()) {
		mes " 请先卸下的你坐骑再尝试!";
		close;
	}
	if (BaseJob == Job_Crusader && job_royal == 5) {
		if (countitem(6275) == 0) {
			mes "^000099看来你遗失了那块盾.回去找海因里希寻求帮助^000000.";
			close;
		}
		mes "[国王斯科米德忒兹的记忆]";
		mes "你是谁?";
		next;
		select("我带来了一个遗物...");
		mes "[国王斯科米德忒兹的记忆]";
		mes "我的遗物?";
		next;
		mes "[国王斯科米德忒兹的记忆]";
		mes "太糟了. 又来了一个被海因里希骗的人.";
		next;
		select("你知道 海因里希?");
		mes "[国王斯科米德忒兹的记忆]";
		mes "他是我的忠诚助手. 他已经继续活了几百年了.";
		next;
		select("他为什么可以活几百年...");
		mes "[国王斯科米德忒兹的记忆]";
		mes "也许他的身体已经像快锈铁一样. 他为了获得长寿发现了一个方法可以从一个身体转移到另一个身体.";
		next;
		mes "[国王斯科米德忒兹的记忆]";
		mes "他不想承认我的死讯所以继续游荡在这个世界, 一直不断的找寻皇家卫士.";
		next;
		select("这块盾有着什么意义呢?");
		mes "[国王斯科米德忒兹的记忆]";
		mes "从海因里希那里得到的盾是在我死前我们的约定.";
		next;
		mes "[国王斯科米德忒兹的记忆]";
		mes "海因里希的使命是发现一个适合的人成为一个皇家卫士, 盾就会成为一份契约.";
		next;
		mes "[国王斯科米德忒兹的记忆]";
		mes "对于他, 这是我对他下达的最后一个命令.";
		next;
		mes "[国王斯科米德忒兹的记忆]";
		mes "现在他不需要再做那么多了, 但就因为这样让他变成了一个傻瓜无法忘记国王最后的命令.";
		next;
		select("那现在该怎么办? 我能离开吗?");
		mes "[国王斯科米德忒兹的记忆]";
		mes "对不起我现在无法送你走.";
		next;
		mes "[国王斯科米德忒兹的记忆]";
		mes "这份和海因里希定下的约定我要保留着. 你看起来是忠诚和勇敢的.";
		next;
		mes "[国王斯科米德忒兹的记忆]";
		mes "现在, 是时候对海因里希实现承诺了. 这个看你了.";
		next;
		mes "[国王斯科米德忒兹的记忆]";
		mes "我计划承认你是皇家卫士的一员, 并继续履行他们的传统. 你是否准备好去做了吗?";
		next;
		if(select("- 是, 当然:- 还没有") == 2) {
			mes "[国王斯科米德忒兹的记忆]";
			mes "嗯, 也许海因里希的判断是错误的.";
			close;
		}
		mes "[国王斯科米德忒兹的记忆]";
		mes "如果这样, 我将授权你成为一名皇家卫士.";
		next;
		if(select("- 太棒了.:- 还不行.") == 2) {
			mes "[国王斯科米德忒兹的记忆]";
			mes "也许海因里希对你的印象是错的.";
			close;
		}
		mes "[国王斯科米德忒兹的记忆]";
		if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
			mes "看看你的身躯有了什么变化!";
			next;
			mes "[国王斯科米德忒兹的记忆]";
			mes "你是不是仍然有其他的想法?";
			mes "你不能继承为皇家卫士!";
			close;
		}
		mes "恭喜! 年轻的皇家卫士.";
		mes "记住这个荣誉的象征继续发扬皇家卫士伟大的传统.";
		close2;
		specialeffect2 EF_MAPPILLAR;
		progressbar "FFFF00",7;
		specialeffect2 EF_LORD;
		delitem 6275,1; //King_Shield
		completequest 12094;
		jobchange roclass(eaclass()|EAJL_THIRD);
		getitem 5757,1; //Schmidt_Helm
		getitem 2795,1; //Green_Apple_Ring
		end;
	}
	end;
}

gl_church,173,88,0	script	挖过的痕迹#01	CLEAR_NPC,{
	if (job_royal == 4 && countitem(6274) == 0) {
		progressbar "ffff00",5;
		set .@roy_ran, rand(1,70);
		if (.@roy_ran < 6)
			getitem 6274,1; //Saint_Cloth_Piece
		else if (.@roy_ran > 5 && .@roy_ran < 66)
			getitem 7049,1; //Stone
		else {
			mes "当你在调查的时候, 地势突然开裂. 最好还是暂时挖掘另外的地方.";
			disablenpc strnpcinfo(0);
			initnpctimer;
			close;
		}
	}
	mes "这里有一个曾经被人挖过的痕迹.";
	close;
OnTimer60000:
	enablenpc strnpcinfo(0);
	stopnpctimer;
OnTimer1000:
OnTimer20000:
OnTimer40000:
	specialeffect EF_MAPPILLAR;
	end;
	specialeffect EF_MAPPILLAR;
	end;
	specialeffect EF_MAPPILLAR;
	end;
}
gl_church,158,103,0	duplicate(挖过的痕迹#01)	挖过的痕迹#02	CLEAR_NPC
gl_church,139,90,0	duplicate(挖过的痕迹#01)	挖过的痕迹#03	CLEAR_NPC
gl_church,126,62,0	duplicate(挖过的痕迹#01)	挖过的痕迹#04	CLEAR_NPC
gl_church,148,5,0	duplicate(挖过的痕迹#01)	挖过的痕迹#05	CLEAR_NPC
gl_church,185,72,0	duplicate(挖过的痕迹#01)	挖过的痕迹#06	CLEAR_NPC
gl_church,185,72,0	duplicate(挖过的痕迹#01)	挖过的痕迹#07	CLEAR_NPC
gl_church,211,94,0	duplicate(挖过的痕迹#01)	挖过的痕迹#08	CLEAR_NPC
gl_church,185,125,0	duplicate(挖过的痕迹#01)	挖过的痕迹#09	CLEAR_NPC
gl_church,143,125,0	duplicate(挖过的痕迹#01)	挖过的痕迹#10	CLEAR_NPC
gl_church,105,115,0	duplicate(挖过的痕迹#01)	挖过的痕迹#11	CLEAR_NPC

gl_church,158,100,0	script	#Clue of traces	HIDDEN_NPC,4,4,{
	end;
OnTouch:
	if (job_royal == 4 && countitem(6274) == 0) {
		specialeffect2 EF_FREEZE;
		mes "我能感觉到周围一股强大的力量. 最好就在这附近探索一下.";
		viewpoint 1,173,88 ,1, 0xFFFF00;
		viewpoint 1,158,103,2, 0xFFFF00;
		viewpoint 1,139,90 ,3, 0xFFFF00;
		viewpoint 1,126,62 ,4, 0xFFFF00;
		viewpoint 1,148,5  ,5, 0xFFFF00;
		viewpoint 1,185,72 ,6, 0xFFFF00;
		viewpoint 1,211,94 ,7, 0xFFFF00;
		viewpoint 1,185,125,8, 0xFFFF00;
		viewpoint 1,143,125,9, 0xFFFF00;
		viewpoint 1,105,115,10,0xFFFF00;
		close;
	}
	end;
}

sec_in02,12,43,3	script	三转骑士巫师	1_M_LIBRARYMASTER,1,1,{
	callfunc "F_GM_NPC";
	switch(select("Royal Guard:Rune Knight:Sorcerer")) {
	case 1:
		mes "1~5";
		input .@input;
		next;
		if (.@input < 1 || .@input > 5) {
			mes "设置调整皇家卫士";
			mes "你能设置的数字在1 到 5.";
		} else {
			set job_royal, .@input;
			mes "完成";
		}
		close;
	case 2:
		mes "1~24";
		input .@input;
		next;
		if (.@input < 1 || .@input > 24) {
			mes "设置调整符文骑士";
			mes "你能设置数字在 1 到 24.";
		} else {
			set job_rune_edq, .@input;
			mes "完成";
		}
		close;
	case 3:
		mes "1~5";
		input .@input;
		next;
		if (.@input < 1 || .@input > 5) {
			mes "设置调整巫师";
			mes "你能设置的数字在 1 到 5.";
		} else {
			set job_soc, .@input;
			mes "完成";
		}
		close;
	}
}
