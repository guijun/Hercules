//===== RoSF.us Script ======================================
//= Sorcerer Job Quest
//===== By: ==================================================
//= Muad_Dib
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= [Official Conversion]
//= Job change Quest from Sage / Professor -> Sorcerer.
//===== Additional Comments: =================================
//= 1.0 First Version. [Masao]
//= 1.1 Cleaning. [Euphy]
//= 1.2 Updated script, optimized. [Euphy]
//============================================================

gef_tower,102,34,5	script	梅里特	2_M_SAGE_B,{
	mes "[梅里特]";
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "抱歉, 您超负重了.";
		close;
	}
	if (Class == Job_Sorcerer || Class == Job_Sorcerer_T || Class == Job_Baby_Sorcerer) {
		mes "你应该继续你的信念!";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "哇~~ 你是位个年轻的冒险家. 我好羡慕你.";
		close;
	}
	if (BaseJob != Job_Sage) {
		mes "啊~~ 我们不需要继续交流下去了.";
		close;
	}
	if (job_soc == 0) {
		mes "欢迎, 有什么可以帮到你吗?";
		next;
		select("- 你知道有关于元素使的事情吗?");
		mes "[梅里特]";
		mes "作为一位贤者不是更加的轻松吗? 元素使每天都要和灵魂交流, 根本没有时间去学习...";
		next;
		mes "[梅里特]";
		mes "为什么你要成为一位元素使?";
		next;
		if(select("- 火的圣灵引导我到这里.:- 水的圣灵引导我到这里.:- 地的圣灵引导我到这里:- 肉的圣灵引导我到这里.") == 4) {
			mes "[梅里特]";
			mes "根本没有这样的圣灵!";
			close;
		}
		mes "[梅里特]";
		mes "哦, 我看看.";
		mes "你天生就知道你可以与灵沟通, 或者你是一个的骗子. ";
		next;
		mes "[梅里特]";
		mes "考虑中.. 那现在你可以写下你的名字?";
		next;
		if(select("- 写名字.:- 我不要.") == 2) {
			mes "[梅里特]";
			mes "那好, 有机会再见面";
			close;
		}
		mes "[梅里特]";
		mes "所以你是 [" + strcharinfo(0) + "].";
		next;
		mes "[Merito]";
		mes "请稍等, 让我登记你的名字.";
		mes "等会再和我说话.";
		set job_soc,1;
		close;
	} else if (job_soc == 1) {
		mes "很好, 让我来介绍你什么事元素使.";
		next;
		mes "[梅里特]";
		mes "[" + strcharinfo(0) + "], 你可以自由的使用魔法?";
		next;
		if(select("- 是的, 我可以.:- 没, 我不是很在行.") == 2) {
			mes "[梅里特]";
			mes "什么!? 你不会使用魔法?";
			mes "那你来这里干嘛? 滚开!";
			close;
		}
		mes "[梅里特]";
		mes "很明显你是.";
		mes "当然, 元素使也是用魔法的. 但是你知道元素使的魔法与其他的魔法有什么分别吗?";
		next;
		switch(select("- 他们知道怎样唱歌.:- 他们都是老顽固.:- 他们知道怎样与灵沟通.")) {
		case 1:
			mes "[梅里特]";
			mes "那不是真正的理由.";
			close;
		case 2:
			mes "[梅里特]";
			mes "什么! 你竟敢!?!";
			close;
		case 3:
			break;
		}
		mes "[梅里特]";
		mes "这是正确的. 这是元素使独有的技能.";
		next;
		mes "[梅里特]";
		mes "当那些人要成为元素使. 是看在他们可以不可以与圣灵沟通.";
		next;
		mes "[梅里特]";
		mes "所以这是很简单的介绍. 所以现在你要拿测试了";
		next;
		mes "[梅里特]";
		mes "是由你来决定. 现在你要怎么做?";
		next;
		switch(select("- 我会拿测验.:- 下一次吧.")) {
		case 1:
			mes "[梅里特]";
			mes "好. ";
			mes "我现在会传送你到4F找卡拉索. 他会考考你元素使的东西.";
			set job_soc,2;
			setquest 12096;
			close;
		case 2:
			mes "[梅里特]";
			mes "什么? 你确定你要成为一位元素使?.";
			close;
		}
	} else {
		mes "你的测验怎样了? 卡拉索 有点懒, 所以...";
		close;
	}
}

gef_tower,113,161,5	script	卡拉索	4_M_SAGE_A,{
	mes "[卡拉索]";
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "超重了";
		close;
	}
	if (Class == Job_Sorcerer || Class == Job_Sorcerer_T || Class == Job_Baby_Sorcerer) {
		mes "你看起来很开心. 祝你有个愉快的旅程.";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "请点完你的技能点?";
		close;
	}
	if (BaseJob != Job_Sage) {
		mes "我是属于元素使协会的. 我不认为你与我有任何的关系.";
		close;
	}
	if (job_soc < 2) {
		mes "越来越多人来吉芬旅游了.";
		close;
	} else if (job_soc == 2) {
		mes "....";
		next;
		select("额..不好意思~");
		mes "[卡拉索]";
		mes "... (Z z z~)";
		next;
		select("Hm hm!!!~");
		mes "[卡拉索]";
		mes "哎呀!! ";
		mes "我的天... ";
		next;
		mes "[卡拉索]";
		mes "你..你是从哪来的?";
		next;
		select("梅里特吩咐我来找你.");
		mes "[卡拉索]";
		mes "我看看, 请稍等.";
		mes "列表...已经到了.";
		next;
		mes "[卡拉索]";
		mes "所以你是 [" + strcharinfo(0) + "]... 对吧?";
		next;
		if(select("是的.:不,我不是!") == 2) {
			mes "[卡拉索]";
			mes "哦.. 是吗.. 那列表写错了?";
			close;
		}
		mes "[卡拉索]";
		mes "在你还没成为一位元素使. 我会告诉你一些关于元素使的东西";
		next;
		mes "[卡拉索]";
		mes "成为一位元素使是非常简单的. 你只需要去寻找散落在世界各地的每一个圣灵, 然后将他们变成的一种物质.";
		next;
		mes "[卡拉索]";
		mes "听起来很简单吧?";
		next;
		select("啊..这有可能吗?");
		mes "[卡拉索]";
		mes "当然, 你不可能一个人办到. 你必须训练自己 然后你才可以清楚的看到他们.";
		next;
		mes "[卡拉索]";
		mes "所以, 我们会进行一定的巡游仪式这样我们才可以感受到圣灵的地方. ";
		next;
		mes "[卡拉索]";
		mes "我会告诉你怎么做. 听好了";
		next;
		mes "[卡拉索]";
		mes "平时我们是看不到圣灵, 但有一种方法可以通过一种特殊的媒介来看见圣灵. ";
		next;
		mes "[卡拉索]";
		mes "例如, 如果您想找风的圣灵, 那你就必须找到有风灵矿石的地方,  然后圣灵就会显示不良反应. ";
		next;
		mes "[卡拉索]";
		mes "同样, 如果您想找火的圣灵, 那你就必须找到有火灵矿石的地方, 然后圣灵就会显示不良反应.";
		next;
		mes "[卡拉索]";
		mes "元素使就使用是用这种方法的, 这是元素使基本的知识. ";
		next;
		select("所以去与灵交流...");
		mes "[卡拉索]";
		mes "好的~";
		mes "这职业很注重与圣灵的交流.";
		next;
		mes "[卡拉索]";
		mes "可是对你这种书虫来说很很困难...";
		next;
		mes "[卡拉索]";
		mes "你现在需要收集一些材料.";
		next;
		mes "[卡拉索]";
		mes "3个风灵矿石";
		mes "3个水灵矿石";
		mes "3个火灵矿石.";
		mes "3个地灵矿石.";
		next;
		mes "[卡拉索]";
		mes "当你收集好了就来找我, 我会告诉你下一步怎么做";
		set job_soc,3;
		changequest 12096,12097;
		close;
	} else if (job_soc == 3) {
		if (countitem(992) > 2 && countitem(991) > 2 && countitem(990) > 2 && countitem(993) > 2) {
			mes "你都收集好了?";
			next;
			select("- 现在我该做些什么?");
			mes "[卡拉索]";
			mes "现在我来告诉你. 你最好拿簿子来写下我所说的东西.";
			next;
			mes "[卡拉索]";
			mes "1. 冰洞穴 1F";
			mes "2. 图尔火山洞窟 1F";
			mes "3. 妙勒尼 废弃矿场 3F";
			mes "4. 昆仑洞穴 2F";
			next;
			mes "[卡拉索]";
			mes "我们在那4个地方都放探测器, 好让你们这些普通人也看得见.";
			next;
			mes "[卡拉索]";
			mes "请注意, 当你有超过一定数额的灵石这些探测器才能会被激活. ";
			next;
			mes "[卡拉索]";
			mes "你必须准备最少3个灵石, 比如:火灵矿石, 水灵矿石, 风灵矿石 和 地灵矿石.";
			next;
			select("所以我必须准备这些灵石...");
			mes "[卡拉索]";
			mes "是的, 你得赶快!";
			next;
			mes "[卡拉索]";
			mes "一旦你把灵石放到探测器, 他们会帮助你跟圣灵交流. ";
			next;
			mes "[卡拉索]";
			mes "如果你成功了, 你可以提取圣灵的一部分, 作为实验. 如果你失败了, 灵石会被不见. ";
			next;
			mes "[卡拉索]";
			mes "如果你幸运的话你就会成功, 如果你失败了, 风灵矿石会变成风灵原石....";
			next;
			select("- 如果我的灵石都用完了呢?");
			mes "[卡拉索]";
			mes "很简单. 你就再得去收集";
			next;
			mes "[卡拉索]";
			mes "所以以防万一那些人都会带很多的灵石去, .";
			next;
			mes "[卡拉索]";
			mes "如果你已经是元素使,  你就不用用这种方法来与圣灵交流.";
			next;
			select("这样的实验是...?");
			mes "[卡拉索]";
			mes "我们也不知道灵里装的是什么. 因为不同的圣灵有不同的属性";
			next;
			mes "[卡拉索]";
			mes "如果你成功与3个圣灵交流就回来找我吧.";
			next;
			mes "[卡拉索]";
			mes "一种试剂不能装不会超过2种圣灵.";
			next;
			mes "[卡拉索]";
			mes "有任何问题吗?";
			next;
			if(select("- 没有.:- 请解释多一次.") == 2) {
				mes "[卡拉索]";
				mes "那我们等会见";
				next;
			}
			mes "[卡拉索]";
			mes "让我总结我刚刚所说的话, 请稍等...";
			next;
			mes "^000099如果有什么问题, 你就开你的任务栏看^000000.";
			set job_soc,4;
			changequest 12097,12098;
			close;
		}
		mes "在还没让你知道下一步改该做什么, 我先去收集我要的东西吧.";
		next;
		mes "[卡拉索]";
		mes "3个风灵矿石.";
		mes "3个水灵矿石.";
		mes "3个火灵矿石.";
		mes "3个地灵矿石.";
		next;
		mes "[卡拉索]";
		mes "当你都收集好了, 再来找我吧.";
		close;
	} else if (job_soc == 4) {
		if (countitem(6276) && countitem(6278) && countitem(6277)) {
			mes "哇, 你的技术好好.";
			mes "我以为你会被灵给侵入, 但是现在你已经成功地变成了药物!";
			next;
			select("这就是我需要做得事?");
			mes "[卡拉索]";
			mes "干得好. 我先去处理这些药. 所以请稍候.";
			delitem 6276,1; //Clear_Reagent
			delitem 6278,1; //Black_Reagent
			delitem 6277,1; //Red_Reagent
			set job_soc,5;
			completequest 12098;
			close;
		}
		mes "与灵交流不是一件简单的是.";
		next;
		mes "[卡拉索]";
		mes "别放弃, 加油~";
		close;
	} else if (job_soc == 5) {
		mes "本来我的老师是你们的转职教官, 可是他现在出去找鱼的灵魂了, 所以现在我是代理教官";
		next;
		mes "[卡拉索]";
		mes "亲爱的 [" + strcharinfo(0) + "],";
		mes "请通过我们所给你作为元素使的测试.";
		next;
		mes "[卡拉索]";
		mes "这当然只是一个开始, 我们不知道你会与什么灵交流.";
		next;
		mes "[卡拉索]";
		mes "但是以你现在的能力你一定能做到的, 加油 [" + strcharinfo(0) + "]";
		next;
		mes "[卡拉索]";
		if (BaseLevel < 99 || JobLevel < 50 || SkillPoint) {
			mes "请检查你的状态. ";
			next;
			mes "[卡拉索]";
			mes "你必须完成所有的测验才能转职! ";
			close;
		}
		if (ismounting()) {
			mes "很好, 在我们还没开始前...";
			mes "请卸下你的坐骑~";
			close;
		}
		mes "你已经成功完成了测试.";
		next;
		mes "[卡拉索]";
		mes "恭喜, [" + strcharinfo(0) + "]";
		jobchange roclass(eaclass()|EAJL_THIRD);
		getitem 5756,1; //Wind_Whisper
		getitem 2795,1; //Green_Apple_Ring
		next;
		mes "[卡拉索]";
		mes "我希望你会与圣灵多多交流.";
		close;
	}
}
-	script	圣灵侦测人员#0	-1,{

	if (strnpcinfo(4) == "thor_v01") setarray .@id[0],990,994,6276,6278;
	else if (strnpcinfo(4) == "ice_dun01") setarray .@id[0],991,995,6278,6276;
	else if (strnpcinfo(4) == "mjo_dun03") setarray .@id[0],993,997,6277,6276;
	else if (strnpcinfo(4) == "gon_dun02") setarray .@id[0],992,996,6277,6278;

	if (job_soc == 4 && countitem(.@id[0]) > 2) {
		progressbar "ffff00",5;
		delitem .@id[0],3; //Boody_Red,Crystal_Blue,Yellow_Live,Wind_Of_Verdure
		set .@soc_ran, rand(1,70);
		if (.@soc_ran < 10) {
			if (countitem(.@id[2]) == 0) {
				mes "你用了不明试剂传达给了一个圣灵.";
				getitem .@id[2],1; //Clear_Reagent,Black_Reagent,Red_Reagent
				close;
			}
			mes "似乎你在灵身上没得到任何的东西";
			close;
		} else if (.@soc_ran > 9 && .@soc_ran < 16) {
			if (countitem(.@id[3]) == 0) {
				mes "你用了不明试剂传达给了一个圣灵.";
				getitem .@id[3],1; //Clear_Reagent,Black_Reagent,Red_Reagent
				close;
			}
			mes "似乎你在灵身上没得到任何的东西.";
			close;
		} else if (.@soc_ran == 56) {
			mes "灵和属性石头的合体变成一个巨大的属性石.";
			getitem .@id[1],1; //Flame_Heart,Mistic_Frozen,Great_Nature,Rough_Wind
			close;
		} else {
			mes "失败了.";
			close;
		}
	}
	mes "我用探测器成功得与圣灵交流";
	if (job_soc == 4 && countitem(.@id[0]) < 3) {
		next;
		mes "如果你用正确的灵石, 你应该可以与圣灵交流.";
	}
	close;
}
thor_v01,64,252,0	duplicate(圣灵侦测人员#0)	圣灵侦测人员#1	2_BOARD2
ice_dun01,274,274,0	duplicate(圣灵侦测人员#0)	圣灵侦测人员#2	2_BOARD2
mjo_dun03,200,141,0	duplicate(圣灵侦测人员#0)	圣灵侦测人员#3	2_BOARD2
gon_dun02,195,190,0	duplicate(圣灵侦测人员#0)	圣灵侦测人员#4	2_BOARD2