//===== Hercules Script ======================================
//= Morroc Raceway
//===== By: ==================================================
//= acky
//===== Current Version: =====================================
//= 1.3
//===== Description: =========================================
//= Lets players race around Morroc (pvp_y_1-5)
//===== Additional Comments: =================================
//= If there are more than 3 players, at least 3 people
//= must finish before a new race can be started.
//=
//= If there are less than 3 players, at least 1 person
//= must finish before a new race can be started.
//=
//= Removed permanent global variables
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Replaced effect numerics with constants. [Samuray22]
//============================================================

//Warps you into race way
morocc,166,105,6	script	极速天使#01	4_F_KAFRA2,{
	mes "[极速天使]";
	mes "你想不想参加 ^0000FF梦罗克竞速赛^000000?";
	next;
	menu "- 是",L_Warp,"- 否",-;
	mes "[极速天使]";
	mes "好吧, 等你什么时候改变主要了随时来找我.";
	close;
L_Warp:
	warp "pvp_y_1-5",165,256;
	close;
}

//Warps you out of raceway
pvp_y_1-5,169,265,5	script	极速天使#02	4_F_KAFRA2,{
	mes "[极速天使]";
	mes "欢迎来到梦罗克竞速赛!";
	next;
	menu "- 情报",-,"- 离开",L_Warp,"- 取消",L_Cancel;
	mes "[极速天使]";
	mes "选手们必须点击开始NPC才可以开启竞赛.";
	next;
	mes "[极速天使]";
	mes "当比赛开始后, 逆时针方向围着梦罗克竞速.";
	next;
	mes "[极速天使]";
	mes "你必须通过所有的关卡 - 不能作弊!";
	close;
L_Warp:
	warp "morocc",165,101;
L_Cancel:
	mes "[极速天使]";
	mes "再见!";
	close;
}

//Counts down and starts race
pvp_y_1-5,145,269,5	script	Starter	4_M_JOB_KNIGHT1,{
	if ($@race != 0) goto L_Started;
	if ($@counting != 0) goto L_Started;
	if ($@racecount == 1) goto L_Started;
L_Menu:
	mes "[比赛开始]";
	mes "请待在我的东面.";
	menu "- 开始竞赛",L_Count,"- 取消",-;
	close;
L_Count:
	set $@counting,1;
	mes "倒计时...";
	addtimer 1000, "Starter::OnCount1000";
	addtimer 2000, "Starter::OnCount2000";
	addtimer 3000, "Starter::OnCount3000";
	addtimer 4000, "Starter::OnCount4000";
	announce strcharinfo(0) + "开始倒计时",1;
	announce "准备好比赛!",1;
	close;

OnCount1000:
	announce "[3]",1;
	end;
OnCount2000:
	announce "[2]",1;
	end;
OnCount3000:
	announce "[1]",1;
	end;
OnCount4000:
	emotion 27;
	specialeffect EF_CHIMTO;
	announce "[GO!]",1;
	set $@race,1;
	set $@position,0;
	set $@counting,0;
	set $@raceid,rand(100000,999999);
	end;

L_Started:
	if ((getmapusers("pvp_y_1-5") < 3) && ($@position > 0)) goto L_Menu;
	if ($@position > 2) goto L_Menu;
	mes "[Starter]";
	mes "比赛进行中";
	close;

OnInit:
	set $@race,0;
	set $@position,0;
	set $@racecount,0;
	end;
}

//Checkpoint 1
pvp_y_1-5,144,262,5	script	起点	HIDDEN_NPC,0,5,{
	end;
OnTouch:
	if (@raceid != $@raceid) goto L_Started;
	if (@race == 6) goto L_Finished;
	if ($@race == 1) goto L_Started;
	mes "比赛还没有开始, 请回起跑线.";
	close;
L_Started:
	set @race,1;
	set @raceid,$@raceid;
	end;
L_Finished:
	mes "你已经完成了比赛.";
	close;
}

//Checkpoint 2
pvp_y_1-5,73,247,5	script	检测点[1]	HIDDEN_NPC,6,6,{
	end;
OnTouch:
	if (@race != 1) goto L_Miss;
	set @race,2;
	announce "[" + strcharinfo(0) +"] 已经到达检测点 [1]",1;
	end;
L_Miss:
	mes "你错过了检测点[1]. 请返回.";
	close;
}

//Checkpoint 3
pvp_y_1-5,77,44,5	script	检测点[2]	HIDDEN_NPC,6,6,{
	end;
OnTouch:
	if (@race != 2) goto L_Miss;
	set @race,3;
	announce "[" + strcharinfo(0) +"] 已经到达检测点 [2]",1;
	end;
L_Miss:
	mes "你错过了检测点[2]. 请返回.";
	close;
}

//Checkpoint 3
pvp_y_1-5,249,60,5	script	检测点[3]	HIDDEN_NPC,6,6,{
	end;
OnTouch:
	if (@race != 3) goto L_Miss;
	set @race,4;
	announce "[" + strcharinfo(0) +"] 已经到达检测点 [3]",1;
	end;
L_Miss:
	mes "你错过了检测点[3]. 请返回.";
	close;
}

//Checkpoint 4
pvp_y_1-5,255,256,5	script	检测点[4]	HIDDEN_NPC,6,6,{
	end;
OnTouch:
	if (@race != 4) goto L_Miss;
	set @race,5;
	announce "[" + strcharinfo(0) +"] 已经到达检测点 [4]",1;
	end;
L_Miss:
	mes "你错过了检测点[4]. 请返回.";
	close;
}

//Finish Line
pvp_y_1-5,174,244,5	script	终点	HIDDEN_NPC,6,6,{
	end;
OnTouch:
	if (@raceid != $@raceid) goto L_WrongRace;
	if (@race != 5) goto L_Miss;
	set @race,6;
	set $@position,$@position+1;
	announce "[" + strcharinfo(0) +"] 已经到达了终点! [排名: " + $@position + "]",1;
	end;
L_Miss:
	mes "你错过了检测点. 请返回.";
	close;
L_WrongRace:
	mes "你不在比赛中.";
	close;
}

//Check Point Marker Flags
pvp_y_1-5,144,267,4	script	起点#01	GUILD_FLAG,{
	end;
}
pvp_y_1-5,144,257,4	script	起点#02	GUILD_FLAG,{
	end;
}
pvp_y_1-5,70,252,3	script	检测点[1]#01	GUILD_FLAG,{
	end;
}
pvp_y_1-5,77,243,3	script	检测点[1]#02	GUILD_FLAG,{
	end;
}
pvp_y_1-5,81,48,1	script	检测点[2]#01	GUILD_FLAG,{
	end;
}
pvp_y_1-5,72,40,1	script	检测点[2]#02	GUILD_FLAG,{
	end;
}
pvp_y_1-5,244,65,7	script	检测点[3]#01	GUILD_FLAG,{
	end;
}
pvp_y_1-5,252,57,7	script	检测点[3]#02	GUILD_FLAG,{
	end;
}
pvp_y_1-5,259,260,5	script	检测点[4]#01	GUILD_FLAG,{
	end;
}
pvp_y_1-5,251,252,5	script	检测点[4]#02	GUILD_FLAG,{
	end;
}
pvp_y_1-5,174,249,4	script	终点#01	GUILD_FLAG,{
	end;
}
pvp_y_1-5,174,238,4	script	终点#02	GUILD_FLAG,{
	end;
}
