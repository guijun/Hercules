//===== Hercules Script ======================================
//= Card Removal NPC
//===== By: =============Translation:=========================
//= TyrNemesis^          安赫尔|20130701[ROSF.US]
//===== Current Version: =====================================
//= 1.2a
//===== Description: =========================================
//= Removes cards from equipped items.
//===== Additional Comments: =================================
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2a Added 'disable_items' command. [Euphy]
//============================================================

prt_in,28,73,4	script	聪明的老女人#eAcustom	1_F_ORIENT_04,{

	set .zenycost,200000; // Set base cost in zeny of the card remover services
	set .percardcost,25000; // set cost per card of the card remover services
	set .faildestroy,1; // Should the card remover have a chance of failure that destroys items? 1 = Yes, 0 = No.

	disable_items;
	mes "[聪明的老女人]";
	mes "你好, 年轻人. 我能帮你把插入装备的卡片拆下来. 有兴趣吗? ";
	next;
	switch(select("- 当然有兴趣.:- 报酬多少?:- 不用.")) {
	case 1:
		mes "[聪明的老女人]";
		mes "好的. 那你要在哪件物品上做试验?";
		next;

		setarray .@position$[1], "- 头部","- 身体","- 左手","- 右手","- 背部","- 鞋子","- 装饰品1","- 装饰品2","- 头部2","- 头部3";
		set .@menu$,"";
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
		{
			if( getequipisequiped(.@i) )
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

			set .@menu$, .@menu$ + ":";
		}
		set .@part,select(.@menu$);
		if(!getequipisequiped(.@part)) {
			mes "[聪明的老女人]";
			mes "年轻人... 你什么都不穿叫我拆什么.";
			close;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "[聪明的老女人]";
			mes "年轻人... 你那件装备上没有卡片. 真搞不懂你想让我拆什么下来.";
			close;
		}
		set .@cardcount,getequipcardcnt(.@part);
		
		if (!checkweight(1202,(.@cardcount+1))) { 
			mes "^3355FF稍等片刻!";
			mes "你已经超负重了.";
			mes "这样的话我可什么都做不了.";
			mes "快去找附近的卡普拉吧.";
			mes "把你身上多余的东西都存掉一点.";
			mes "然后我们再继续~";
			close;
		}
		mes "[聪明的老女人]";
		mes "这件物品有" + .@cardcount + "张卡片. 为了施法所需, 你要再准备" + (.zenycost+(.@cardcount * .percardcost)) + " Zeny, 1个^0000FF星星的角^000000, 和一个^0000FF黄色魔力矿石^000000.";
		next;
		if(select("- 好的,来吧.:- 算了.") == 2) {
			mes "[聪明的老女人]";
			mes "好吧. 等你想拆的时候再来找我.";
			close;
		}
		if((Zeny < (.zenycost+(.@cardcount * .percardcost))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
			mes "[聪明的老女人]";
			mes "你身上没有任何东西值得我去施展拆卡法术, 孩子. 等你有需要的时候再来吧.";
			close;
		}
		mes "[聪明的老女人]";
		mes "施法前我必须警告你：拆卡可能会失败. 如果发生这种情况, 可能装备或者卡片, 又或者他们全部都会毁掉. 我可不会赔你哦. 既然这样我们需要先做个最坏打算, 你告诉我, 对你来说卡片和装备哪个更重要? ";
		next;
		switch(select("- 我改主意了:- 装备:- 卡片.")) {
		case 1:
			mes "[聪明的老女人]";
			mes "好吧. 等你想拆的时候记得来找我.";
			close;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "[聪明的老女人]";
		mes "好的, 我要开始了.";
		set Zeny, Zeny - (.zenycost+(.@cardcount * .percardcost));
		delitem 1000,1;
		delitem 715,1;
		
		// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
		// First value = Total failure chance (item and cards destroyed)
		// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
		// Third value = Harmless failure chance (all that's lost is your investment)

		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[聪明的老女人]";
				mes "完全失败了! 可能所有东西都坏了.";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "[聪明的老女人]";
					mes "我试图将卡片拆除的时候, 卡片坏了, 但是装备还是好的.";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[聪明的老女人]";
					mes "很不幸, 拆卡是成功了, 但是装备本身坏了.";
					close;
				}
			}
		}

		if(.@failchance < 10) {
			next;
			failedremovecards .@part,3;
			mes "[聪明的老女人]";
			mes "拆卡失败了. 很幸运的是, 他们都没有损坏.";
			close;
		}
		next;
		successremovecards .@part;
		mes "[聪明的老女人]";
		mes "拆卡成功, 这是你的卡片和装备. 再见.";
		close;
	case 2:
		mes "[聪明的老女人]";
		mes "我收取固定费用 "+.zenycost+" Zeny, 加 "+.percardcost+" Zeny 每张拆下来的卡片. 另外, 我还需要1个星星的角和1个黄色魔力矿石来进行施法.";
		close;
	case 3:
		mes "[聪明的老女人]";
		mes "没问题. 如果有需要的时候再来找我吧.";
		close;
	}
}
