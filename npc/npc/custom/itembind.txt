//===== Hercules Script =======================================
//= Item Bind Script
//===== By: ==================================================
//= Akinari
//===== Compatible With: =====================================
//= Revision 17351+ (rAthena)
//= Revision 12949+ (Hercules)
//===== Description: =========================================
//= Item Bind Script
//= Allows users to pay a price to make an item bound to
//= Account, Character, or Guild
//============================================================

prontera,164,101,3	script	装备绑定服务	4_M_JP_MID,{

	mes "我可以帮你制作绑定装备, 例如帐号, 公会, 或者角色"+((.bindprice)?" 仅需要 ^0000FF"+.bindprice+"^000000 Zeny":"")+".";
	next;
	mes "当然, 我可以保证你的装备安全.";
	next;
	mes "你希望如何操作?";
	if(select("- 绑定:- 解除绑定") == 1) {
		if(Zeny < .bindprice) {
			mes "抱歉您所携带的Zeny不够.";
			close;
		}
		mes "选择绑定类型?";
		.@boundtype = 1 << (select("- 帐号方式:- 公会方式:- 角色方式")-1);
		if(.@boundtype == 2 && (!getcharid(2) || getguildmaster(getcharid(2)) != strcharinfo(0))) {
			mes "如果需要我制作公会绑定物品, 你需要是公会会长才行.";
			close;
		}
		getinventorylist();
		for(.@i = 0; .@i < @inventorylist_count; .@i++) {
			//We only show the items that you allow to be bound
			//Allows equipment (default)
			if(@inventorylist_bound[.@i])
				continue;
			if(((.allowbind & 1) && (getiteminfo(@inventorylist_id[.@i],2) == (4|5))) || 
				((.allowbind & 2) && (getiteminfo(@inventorylist_id[.@i],2) == (0|2|11|18))) ||
				((.allowbind & 4) && (getiteminfo(@inventorylist_id[.@i],2) == (3|6|7|8|10)))
			) {
				set .@bindlist$, .@bindlist$ + ":" + getitemname(@inventorylist_id[.@i]) + " - " + @inventorylist_id[.@i];
				set .@bindlist[.@j],.@i;
				.@j++;
			}
		}
		.@item = .@bindlist[select(.@bindlist$)-2];
		next;
		mes "在我开始之前, 我想让你知道操作时候的一个要点.  如果你有一件特殊的装备需要限制, 请从背包中先移除重复的装备.";
		if(select("- 我明白, 请继续:- 稍等") == 2) {
			next;
			mes "请准备好再来.";
			close;
		}
		if (getequipexpiretick(.@part)) {
            mes "抱歉, 我无法对租赁物品进行操作.";
            emotion e_sry;
            close;
        }
		next;
		mes "你希望将 "+ getitemname(@inventorylist_id[.@item]) +" 设置为 "+.boundtypes$[.@boundtype]+"绑定物品?";
		if(select("- 是:- 否") == 1) {
			Zeny -= .bindprice;
			delitem2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item];
			getitembound2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item],.@boundtype;
			mes "全部完成了!";
			if(.logbinds)
				logmes "将 "+ @inventorylist_amount[.@item]+" "+@inventorylist_id[.@item]+" 设置为 "+.boundtypes$[.@boundtype]+" 类型.";
		}
	} else {
		if(!countbound()) {
			mes "你的背包中没有任何绑定物品. 我什么都做不了.";
			close;
		}
		countbound(2);
		if(.unbindprice) {
			mes "解除物品绑定的费用是 ^0000FF"+.unbindprice+"^000000 zeny.";
			if(Zeny < .unbindprice) {
				mes "你准备的费用不足.";
				close;
			}
		}
		if (getequipexpiretick(.@part)) {
            mes "抱歉, 我无法对租赁物品进行操作.";
            emotion e_sry;
            close;
        }
		getinventorylist();
		for(.@i = 0; .@i < @inventorylist_count; .@i++) {
			if(@inventorylist_bound[.@i]) {
				set .@bindlist$, .@bindlist$ + ":" + getitemname(@inventorylist_id[.@i]) + " - " + @inventorylist_id[.@i];
				set .@bindlist[.@j],.@i;
				.@j++;
			}
		}
		.@item = .@bindlist[select(.@bindlist$)-2];
		next;
		for(.@i = 0; .@i < getarraysize(@bound_items); .@i++) {
			if(@inventorylist_id[.@item] == @bound_items[.@i] &&
				(!getcharid(2) || getguildmaster(getcharid(2)) != strcharinfo(0))
			) {
				mes "我只能为公会会长解除公会绑定设置.";
				close;
			}
		}
		mes "在我开始之前, 我想让你知道操作时候的一个要点.  如果你有一件特殊的装备需要限制, 请从背包中先移除重复的装备.";
		if(select("- 我明白, 请继续:- 稍等") == 2) {
			next;
			mes "请准备好再来.";
			close;
		}
		next;
		mes "你确定需要将 "+ getitemname(@inventorylist_id[.@item]) +"解除绑定?";
		if(select("- 是:- 否") == 1) {
			Zeny -= .unbindprice;
			delitem2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item];
			getitem2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item];
			mes "All done!";
			if(.logbinds)
				logmes "解除 "+ @inventorylist_amount[.@item]+" "+@inventorylist_id[.@item]+"绑定.";
		}
	}
	close;

OnInit:
	//* Configuration *\\
	//Price
	.bindprice = 0;
	.unbindprice = 100000;

	//What to allow to be bound - Add as necessary
	//1 = Equipment - 2 = Consumables - 4 = Etc
	.allowbind = 1;

	//Log binds via NPC?
	.logbinds = 1;

	//Other stuff
	.boundtypes$[1] = "account";
	.boundtypes$[2] = "guild";
	.boundtypes$[4] = "character";
	end;
}
